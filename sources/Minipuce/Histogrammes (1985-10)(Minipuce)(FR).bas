10 ! ----------------------------------
20 !         HISTOGRAMME
30 ! ----------------------------------
40 ! CET UTILITAIRE QUE VOUS POURREZ INTEGER EN S/PROGRAMME DANS VOS
50 ! PROGRAGMES VA VOUS PERMETTRE DE VISUALISER GRAPHIQUEMENT LA
60 ! REPARTITION DE 2 A 7 ELEMENTS AVEC UN INTITULE ET LE POURCENTAGE
70 ! CORRESPONDANT.
80 ! AU MOMENT DE L'APPEL, VOUS DEVEZ AVOIR INITIALISE 4 PARAMETRES :
90 ! -1- LA ZONE NUMERIQUE NBRE DOIT CONTENIR UNE VALEUR COMPRISE
100 !    ENTRE 2 ET 7 CORRESPONDANT AU NOMBRE D'ELEMENTS A VISUALISER
110 ! -2- LA TABLE NB DONT LE NOMBRE DE POSTES DOIT ETRE EGAL A LA
120 !    VALEUR STOCKEE DANS NBRE.
130 !    CHACUN CONTENANT LE POURCENTAGED'UN ELEMENT
140 ! -3- LA TABLE CHAINE LIB$ CONTENANT DANS CHACUN DE CES POSTES LE
150 !    LIBELLE D'UN ELEMENT
160 ! -4- LE TITRE DE L'HISTOGRAMME DANS TX$
170 !  EN CAS DE PARAMETRES INCORRECTS, UN CODE RETOUR DIFFERENT DE
180 !    ZERO VOUS SERA RETOURNE DANS LAZONE RC :
190 !  4 ==> NOMBRE D'ELEMENTS < 2 OU > 7
200 !  8 ==> TOTAL DES POSTES DE NB DIFFERENTS DE 100
210 !  APRES LA REPRESENTATION A L'ECRANDE L'HISTOGRAMME APPUYER SUR "RC"
220 !  POUR REVENIR DANS VOTRE PROGRAMME
230 !
240 !   ***  EXEMPLE D'UTILISATION  ***
250 !
260 ! VOTRE PROGRAMME A CALCULE LA REPARTITION D'UNE POPULATION PAR
270 ! TRANCHE D'AGE DE LA FACON SUIVANTE
280 !
290 !   -1- -20 ANS    : 32%
300 !   -2- 20 A 64 ANS  : 55%
310 !   -3- + DE 64 ANS  : 13%
320 ! INITIALISER DE CETTE FACON :
330 !
340 NBRE=3:!   NOMBRE D'ELEMENTS
350 NB(1)=32:!    REPARTITION
360 NB(2)=55:!        PAR
370 NB(3)=13:!    CATEGORIES
380 LIB$(1)="-20ANS"
390 LIB$(2)="20-64ANS"
400 LIB$(3)="+64ANS"
410 TX$="REPARTITION DE LA POPULATION"
420 CLS
430 GOSUB 30470
440 STOP
30440 !
30450 ! ***** DEBUT S/PROGRAMME *****
30460 !
30470 IF NBRE<2 OR NBRE>7 THEN RC=4:GOTO 31470
30480 ON NBRE-1 GOSUB 30650,30710,30770,30830,30890,30950
30490 GOTO 31060
30500 !
30510 ! **** SAUT DE N DATA ****
30520 !
30530 FOR I=I TO J
30540 READ A
30550 NEXT I
30560 RETURN
30570 !
30580 ! **** CHARGT N POSTES DANS LA TABLE *****
30590 !
30600 FOR I=I TO J
30610 READ A
30620 T(I)=A
30630 NEXT I
30640 RETURN
30650 I=1:J=25
30660 GOSUB 30510
30670 READ A,B
30680 T(1)=A:T(2)=B
30690 LARG=18
30700 RETURN
30710 I=1:J=22
30720 GOSUB 30510
30730 I=1:J=3
30740 GOSUB 30580
30750 LARG=12
30760 RETURN
30770 I=1:J=18
30780 GOSUB 30510
30790 I=1:J=4
30800 GOSUB 30580
30810 LARG=9
30820 RETURN
30830 I=1:J=13
30840 GOSUB 30510
30850 I=1:J=5
30860 GOSUB 30580
30870 LARG=7
30880 RETURN
30890 I=1:J=7
30900 GOSUB 30510
30910 I=1:J=6
30920 GOSUB 30580
30930 LARG=5
30940 RETURN
30950 I=1:J=7
30960 GOSUB 30580
30970 LARG=4
30980 RETURN
30990 DATA 0,6,12,18,24,30,36
31000 DATA 0,7,14,21,28,35
31010 DATA 0,8,16,24,32
31020 DATA 0,10,20,30
31030 DATA 0,14,28
31040 DATA 0,22
31050 !
31060 !   CONTROLE CUMUL POURCENTAGES
31070 !
31080 CUM=0
31090 FOR I=1 TO NBRE
31100 CUM=NB(I)+CUM
31110 NEXT I
31120 IF CUM<>100 THEN RC=8:GOTO 31470
31130 !
31140 !   AFFICHAGE
31150 !
31160 CALL HRON("W",6,13)
31170 LOCATE (2,6):PRINT TX$
31180 FOR I=1 TO NBRE
31190 CCD=T(I)*8:LL=119:CCA=CCD+(LARG*8)-1
31200 FOR J=1 TO NB(I)
31210 CALL LINE("R",CCD,LL,CCA,LL)
31220 LL=LL-1
31230 NEXT J
31240 NEXT I
31250 !
31260 !   ECRITURE DES POUCENTAGES
31270 !
31280 LL=3
31290 CALL COLOR("0BY")
31300 FOR I=1 TO NBRE:LOCATE (LL+1,T(I)+(LARG/2)-1)
31310 PRINT NB(I)
31320 NEXT I
31330 !
31340 !  ECRITURE DES LIBELLES
31350 !
31360 LL=20
31370 CALL COLOR("0WG")
31380 FOR I=1 TO NBRE
31390 IF LEN(LIB$(I))>LARG THEN ZZP$=SEG$(LIB$(I),1,LARG):GOTO 31440
31400 IF LEN(LIB$(I))=LARG THEN ZZP$=LIB$(I):GOTO 31440
31410 ZZDIF=LARG-LEN(LIB$(I))
31420 ZZDIF=ZZDIF/2
31430 LOCATE (LL,T(I)+ZZDIF):PRINT LIB$(I):GOTO 31450
31440 LOCATE (LL,T(I)+1):PRINT ZZP$
31450 NEXT I
31460 CALL COLOR("0BC")
31470 PAUSE:CALL HROFF
31480 RETURN
