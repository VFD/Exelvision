100 !-----------------------------------
110 !             DATES
120 !-----------------------------------
130 DIM T1(8)
140 DATA 0,1,2,3,4,5,6,0
150 FOR I=1 TO 8
160 READ T1(I)
170 NEXT I
180 DIM T21(12)
190 DATA 1,4,4,7,2,5,7,3,6,1,4,6
200 FOR I=1 TO 12
210 READ T21(I)
220 NEXT I
230 DIM T22(13)
240 DATA 7,1,2,3,4,5,6,7,1,2,3,4,5
250 FOR I=1 TO 13
260 READ T22(I)
270 NEXT I
280 DIM ZZ$(12)
290 DATA JANVIER,FEVRIER,MARS,AVRIL,MAI,JUIN,JUILLET,AOUT,SEPTEMBRE,OCTOBRE
300 DATA NOVEMBRE,DECEMBRE
310 FOR I=1 TO 12
320 READ ZZ$(I)
330 NEXT I
340 DIM ZZ2$(13)
360 FOR I=1 TO 13
370 READ ZZ2$(I)
380 NEXT I
390 CLS "BMb"
400 CALL COLOR("0BM")
410 LOCATE (4,15)
420 PRINT "QUEL JOUR ?"
430 PRINT TAB(15);"***********"
440 LOCATE (8,11)
450 PRINT "   Ce programme vous permet de savoir"
460 PRINT "quel jour de la semaine s'est produit"
470 PRINT "un  evenement  dont  vous  connaissez"
480 PRINT "la date."
490 NC$=KEY$
500 CLS "BbM"
510 CALL COLOR("0Bb")
520 LOCATE (4,15)
530 PRINT "QUEL JOUR ?"
540 PRINT TAB(15);"***********"
550 LOCATE (10,6)
560 PRINT "ENTREZ UNE DATE (MOIS EN TOUTES LETTRES)"
570 IND1=0:IND2=0:IND3=0
580 ZZ(1)=13:ZZ(2)=5:ZZ(3)=2:ZZ(4)=1
590 GOSUB 900:JJ=ZZI
600 ZZ(1)=13:ZZ(2)=8:ZZ(3)=9:ZZ(4)=2
610 GOSUB 900
620 FOR S=1 TO 12
630 IF ZZI$=ZZ$(S)THEN MM=S:GOTO 650 ELSE NEXT S
640 IF S>12 THEN 550
650 MS$=ZZI$:ZZ(1)=13:ZZ(2)=18:ZZ(3)=2:ZZ(4)=1
660 ZZ(1)=13:ZZ(2)=18:ZZ(3)=2:ZZ(4)=1
670 GOSUB 900:AA2=ZZI
680 ZZ(1)=13:ZZ(2)=20:ZZ(3)=2:ZZ(4)=1:GOSUB 900:AA=ZZI
690 ZDATE=(AA2*100)+AA
700 IF ZDATE<1583 THEN 660
710 NBRB=INT(AA/4)
720 RST=AA-(NBRB*4)
730 IF RST>0 THEN 740 ELSE IF MM>2 THEN 740 ELSE IF MM=2 THEN MM=8 ELSE MM=4
740 DVD=AA+NBRB+1
750 QUT=INT(DVD/7)
760 IND1=DVD-(QUT*7)
770 IF AA2=19 THEN IND1=IND1+2 ELSE IND1=IND1+1
780 IND2=IND2+T1(IND1)
790 IND2=IND2+T21(MM)
800 IND3=T22(IND2)+IND3
810 QUT=INT(JJ/7)
820 RST=JJ-(QUT*7)
830 IND3=IND3+RST
840 LOCATE (18,3)
850 PRINT "LE";JJ;MS$;ZDATE;"EST UN ";ZZ2$(IND3)
860 LOCATE (22,1):PRINT "VOULEZ VOUS ENTRER UNE AUTRE DATE (O/N)?"
870 R$=KEY$
880 IF R$="O" THEN 500
890 END
900 ZZRC=0
910 IF ZZ(1)>22 THEN ZZRC=4:RETURN
920 IF ZZ(2)>40 THEN ZZRC=8:RETURN
930 IF ZZ(3)>41 THEN ZZRC=12:RETURN
940 IF ZZ(3)+ZZ(2)>40 THEN ZZRC=16:RETURN
950 IF ZZ(4)<>1 AND ZZ(4)<>2 AND ZZ(4)<>3 THEN ZZRC=20:RETURN
960 LOCATE (ZZ(1),ZZ(2))
970 ZZC=ZZ(2)
980 ZZS=ZZ(2)+ZZ(3)-1
990 IF ZZ(4)=1 THEN GOSUB 1010:RETURN
1000 IF ZZ(4)=2 THEN GOSUB 1130:RETURN
1010 !
1020 ZZI=0:ZZI$=""
1030 ZZA$=KEY$
1040 IF ASC(ZZA$)=13 THEN RETURN
1050 IF ASC(ZZA$)<48 OR ASC(ZZA$)>57 THEN LOCATE (ZZ(1),ZZC):GOTO 1030
1060 ZZI$=ZZI$&(ZZA$):ZZI=VAL(ZZI$)
1070 LOCATE (ZZ(1),ZZC)
1080 PRINT (ZZA$)
1090 ZZC=ZZC+1
1100 IF ZZC>ZZS THEN RETURN
1110 LOCATE (ZZ(1),ZZC)
1120 GOTO 1030
1130 !
1140 ZZI$=""
1150 ZZA$=KEY$
1160 IF ASC(ZZA$)=13 THEN RETURN
1170 IF ASC(ZZA$)<65 OR ASC(ZZA$)>90 THEN GOTO 1180 ELSE GOTO 1190
1180 IF ASC(ZZA$)<>32 OR ASC(ZZA$)<>45 THEN LOCATE (ZZ(1),ZZC):GOTO 1150
1190 ZZI$=ZZI$&ZZA$
1200 LOCATE (ZZ(1),ZZC)
1210 PRINT ZZA$
1220 ZZC=ZZC+1
1230 IF ZZC>ZZS THEN RETURN
1240 LOCATE (ZZ(1),ZZC)
1250 GOTO 1150
1255 DATA SAMEDI,DIMANCHE,LUNDI,MARDI,MERCREDI,JEUDI
1260 DATA VENDREDI,SAMEDI,DIMANCHE,LUNDI,MARDI,MERCREDI,JEUDI
