10 !------------------------------------
20 !       PRONOSTICS TIERCE
30 !------------------------------------
40 DIM PR(6),VI(30,2)
50 DIM ST(30)
60 TOC*="0D7C91B2BAB602FC"
70 CLS "bYB":CALL COLOR ("0bY")
80 FOR X=1 TO 30
90 ST(X)=0:VI(X,1)=0:VI(X,2)=0
100 NEXT X
110 TX*="LES PRONOSTICS TIERCE DE L'EXL 100 "
120 GOSUB 1310
130 CLS
140 LOCATE (2,2)
150 PRINT "Je vais essayer de vous faire gagner."
160 LOCATE (4,2)
170 PRINT "Prenez votre journal de pronostics."
180 LOCATE (6,2):PRINT" Entrez pour chaque combinaison les"
190 LOCATE (7,5):PRINT"  numeros des chevaux."
200 PRINT "  Entrez en un maximum.":PRINT ""
210 PRINT "  Apres chaque pronostic,je vous"
220 PRINT "  demanderais votre appreciation"
230 PRINT "  (Note de 1 a 10) sur le pronostiqueur. "
240 PRINT "":PRINT "  On peut commencer vous savez tout."
250 LOCATE (20,8)
260 PRINT "Appuyer sur une touche pour commencer .."
270 A$=KEY$
280 CLS:LOCATE (2,2)
290 PRINT "Combien de chevaux dans la course ? "
300 LOCATE (4,8):INPUT "(de 6 a 30) = ";CH$
310 IF NOT NUMERIC(CH$)THEN GOTO 300
320 CH=VAL(CH$)
330 IF CH<6 OR CH>30 THEN GOTO 280
340 LOCATE (6,2)
350 INPUT "Combien de pronostics ? " ; PN$
360 IF NOT NUMERIC(PN$)THEN GOTO 340
370 PN=VAL(PN$)
380 IF PN<1 THEN GOTO 340
390 FOR B=1 TO PN
400 CLS:LOCATE (2,2)
410 PRINT "Enregistrement pronostic numero ";B
420 RESTORE 1440
430 LOCATE (4,2)
440 PRINT "Entrez les numeros des six premiers"
450 PRINT "       chevaux."
460 L=7
470 FOR C=1 TO 6
480 READ PO$
490 LOCATE (L,2):PRINT PO$;" Cheval :";
500 INPUT " ";PR$
510 IF NOT NUMERIC(PR$)THEN GOTO 490
520 PR(C)=VAL(PR$)
530 IF C=1 THEN GOTO 580
540 FOR Z=1 TO C-1
550 IF PR(Z)=PR(C)THEN A=1
560 NEXT Z
570 IF A=1 THEN A=0:GOTO 490
580 IF PR(C)<1 OR PR(C)>CH THEN GOTO 490
590 L=L+2
600 NEXT C
610 LOCATE (20,2):INPUT "Appreciation (Note de 1 a 10) ";AP$
620 IF NOT NUMERIC(AP$)THEN GOTO 610
630 AP=VAL(AP$)
640 IF AP<1 OR AP>10 THEN GOTO 610
650 PRINT " ":PRINT " Entrees correctes (O/N) ":EN$=KEY$
660 IF EN$="O"OR EN$="o"THEN GOTO 690
670 LOCATE (22,2):PRINT "Corrigez le pronostic     "
680 PAUSE 1:GOTO 400
690 FOR D=1 TO 6
700 ST(PR(D))=ST(PR(D))+((7-D)*5)+AP
710 NEXT D
720 NEXT B
730 CLS:LOCATE (12,5)
740 PRINT "Un moment, je reflechis."
750 FOR X=1 TO CH
760 MAX=0
770 FOR Y=1 TO CH
780 IF MAX>ST(Y)THEN GOTO 810
790 MAX=ST(Y):VI(X,1)=Y
800 VI(X,2)=ST(Y)
810 NEXT Y
820 ST(VI(X,1))=-X
830 NEXT X
840 CLS:LOCATE (12,2)
850 PRINT "Les pronostics tierce de l'EXL 100."
860 LOCATE (20,8):PRINT "Appuyer sur une touche ..."
870 A$=KEY$
880 RESTORE 1420
890 X=0
900 CALL HRON("R",4,1)
910 CLS:LOCATE (2,5)
920 PRINT "VOICI MES PRONOSTICS "
930 LOCATE (3,5)
940 PRINT "--------------------"
950 Y=CH-(INT(CH/6)*6)
960 IF Y<>0 THEN Y=1
970 Y=Y+(CH/6)
980 FOR A=1 TO Y
990 CLS:LOCATE (7,2)
1000 FOR B=1 TO 6
1010 READ PL
1020 IF PL>CH THEN GOTO 1070
1030 X=X+1
1040 PRINT TAB(2)PL;:PRINT TAB(5)" Le cheval numero :";VI(X,1);
1050 PRINT " note =";VI(X,2)
1060 PRINT " "
1070 NEXT B
1080 LOCATE (20,2):PRINT "Appuyer sur une touche ..."
1090 A$=KEY$
1100 NEXT A
1110 CALL HROFF
1120 CLS:LOCATE (2,2)
1130 PRINT "Que voulez vous faire ? "
1140 LOCATE (6,8)
1150 PRINT "- R = Relire les pronostics."
1160 LOCATE (9,8)
1170 PRINT "- C = Autres pronostics."
1180 LOCATE (12,8)
1190 PRINT "- F = Sortir du programme."
1200 R2$=KEY$
1210 IF R2$="R"OR R2$="r"THEN GOTO 840
1220 IF R2$="C"OR R2$="c"THEN GOTO 70
1230 IF R2$<>"F"AND R2$<>"f"THEN GOTO 1200
1240 CLS:LOCATE (12,8)
1250 CALL COLOR("0RWHL")
1260 PRINT "BBOONNEE CCHHAANNCCEE"
1270 LOCATE (13,8)
1280 PRINT "BBOONNNNEE CCHHAANNCCEE"
1290 PAUSE 2:CLS "BCC"
1300 END
1310 ! *** EDITION CHAINE ***
1320 CLS
1330 LOCATE (12,2)
1340 FOR I=1 TO LEN(TX$)
1350 PRINT SEG$(TX$,I,1);
1360 CALL SPEECH("L,"&TOC$)
1370 CALL SPEECH("A,"&TOC$)
1380 NEXT I
1390 CALL KEY1(A,B)
1400 IF B=0 THEN GOTO 1320
1410 RETURN
1420 DATA 1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,21,22,23,24
1430 DATA 25,26,27,28,29,30
1440 DATA "PREMIER  ","DEUXIEME ",TROISIEME,QUATRIEME,CINQUIEME,"SIXIEME  "
