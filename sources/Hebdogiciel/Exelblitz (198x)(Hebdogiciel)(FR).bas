1 !-------------------------------------
2 !         EXELBLITZ
3 ! (C) 1984 JOCELYN DONZE
4 !-------------------------------------
5 GOSUB 160
6 CLS"BCC"
7 Z=20:V=3:TA=0:HA=.4
8 !
9 ! DEFINITION
10 !
11 CALL CHAR(1,"0000F07038F87C3EFF1F")
12 CALL CHAR(2,"0000030F3F7F7F7FFFE3")
13 CALL CHAR(3,"0000C0F0FCFEFEFEFFC7")
14 CALL CHAR(4,"00000F0E1C1F3E7CFFF8")
15 CALL CHAR(5,"07")
16 CALL CHAR(6,"E3EB6F633F3F07")
17 CALL CHAR(7,"C7D7F6C6FCFCE")
18 CALL CHAR(8,"E0")
19 CALL CHAR(9,"00000000000000000007")
20 CALL CHAR(10,"000000000000000000E0")
21 CALL CHAR(11,"077F3E7CF83878E84400")
22 CALL CHAR(12,"E0FE7C3E1F1C1E172200")
23 CALL CHAR(13,"C2C2C3C7CFFFFFCFC7C3")
24 CALL CHAR(14,"4343C3E3F3FFFFF3E3C3")
25 CALL CHAR(15,"0006060606060F0F0909")
26 !
27 ! BRUIT
28 !
29 EXP1$="0BA02163031B18008D1E139840073A108100F0901E01080480478F084420020570CE"
30 EXP2$="BD02110840704E118840043CE0010F78C00316B080052C20010948800214A0000528"
31 EXP$=EXP1$&EXP2$
32 BAT1$="0398A4AD0165B50D20A7F206E439D180BC263AA0803C2704906BB90418107B390718"
33 BAT2$="105B190162AB24406C190888B91C0004106B8500624E23409C190888AD1C013197FF"
34 BAT$=BAT1$&BAT2$
35 CAR1$="077C312DD46D6F708213EC60072B58C10C66303211F4A0072D6841823E9316EAB637"
36 CAR2$="BAD18D6E74A2139DE8443BDAD18E7634A319CD6846339AB18C6634A2118D68442D6A"
37 CAR3$="518B5AD4A21AD5A84635AA518D4A54A21295A844294AD1FF"
38 CAR$=CAR1$&CAR2$&CAR3$
39 !
40 ! ECRAN
41 !
42 TA=TA+1:HA=HA+.05:S=4
43 CLS
44 CALL SPEECH("R,"&BAT$)
45 CALL COLOR("0GG")
46 LOCATE (22,1)
47 PRINT "                                       "
48 CALL COLOR("0BC"):LOCATE (1,1)
49 FOR I=1 TO V:PRINT "# ";:NEXT I
50 LOCATE (2,1):PRINT "TABLEAU N.";TA
51 CALL COLOR("1bC"):LOCATE (1,15)
52 FOR I=1 TO S
53 PRINT CHR$(1);CHR$(2);CHR$(3);CHR$(4);"  ";
54 NEXT I
55 LOCATE (2,15)
56 FOR I=1 TO S
57 PRINT CHR$(5);CHR$(6);CHR$(7);CHR$(8);"  ";
58 NEXT I
59 Y=S*6+9:X=1
60 !
61 ! PREMIER CYCLE
62 !
63 CALL COLOR("1RC")
64 X=X+1
65 Y=Y+SGN(Z-Y-1)
66 LOCATE (X-1,Y-1):PRINT "        "
67 LOCATE (X,Y)
68 PRINT " ";CHR$(1);CHR$(2);CHR$(3);CHR$(4);" "
69 LOCATE (X+1,Y+1)
70 PRINT CHR$(5);CHR$(6);CHR$(7);CHR$(8)
71 CALL COLOR("1MC")
72 CALL KEY1(A,B)
73 Z=Z+(A=131 AND Z>1)-(A=129 AND Z<35)
74 LOCATE (21,Z)
75 PRINT " ";CHR$(13);CHR$(14);" "
76 IF A=32 AND B=1 THEN GOSUB 128
77 !
78 ! DEUXIEMME CYCLE
79 !
80 CALL COLOR("1BC")
81 LOCATE (X,Y+1)
82 PRINT CHR$(9);CHR$(2);CHR$(3);CHR$(10)
83 LOCATE (X+1,Y+1)
84 PRINT CHR$(11);CHR$(6);CHR$(7);CHR$(12)
85 CALL KEY1(A,B)
86 Z=Z+(A=131 AND Z>1)-(A=129 AND Z<35)
87 CALL COLOR("1MC"):LOCATE (21,Z)
88 PRINT " ";CHR$(13);CHR$(14);" "
89 IF A=32 AND B=1 THEN GOSUB 128
90 IF RND<HA THEN GOSUB 93
91 IF X=20 THEN GOSUB 103
92 GOTO 60
93 !
94 ! PROJECTILES ENNEMIS
95 !
96 CALL COLOR("0BC")
97 FOR I=X+2 TO 20 STEP 2
98 LOCATE (I,Y+2):PRINT CHR$(1)
99 LOCATE (I,Y+2):PRINT " "
100 NEXT I
101 IF Y=Z OR Y+1=Z THEN 103
102 RETURN
103 CALL SPEECH("A,"&BAT$)
104 CALL SPEECH("L,"&EXP$)
105 CALL COLOR("0RR")
106 LOCATE (22,1)
107 PRINT "                                       "
108 CALL COLOR("0MB")
109 LOCATE (21,Z+1)
110 PRINT CHR$(12);CHR$(12)
111 PAUSE .4
112 CALL COLOR("0GG")
113 LOCATE (22,1)
114 PRINT "                                       "
115 CALL SPEECH("A,"&EXP$)
116 PAUSE 1
117 V=V-1:IF V<>0 THEN GOTO 43
118 CALL COLOR("0RbHLF")
119 LOCATE (11,16):PRINT "PPEERRDDUU"
120 LOCATE (12,16):PRINT "PPEERRDDUU"
121 LOCATE (1,1):PRINT " "
122 CALL COLOR("0MC")
123 LOCATE (14,12)
124 PRINT "VOULEZ-VOUS REJOUER ?"
125 K$=KEY$
126 IF K$="O" THEN GOTO 6
127 END
128 !
129 ! PROJECTILES DEFENSEUR
130 !
131 CALL COLOR("1BC")
132 FOR I=20 TO X+2 STEP -2
133 LOCATE (I,Z+1):PRINT CHR$(15)
134 LOCATE (I,Z+1):PRINT " "
135 NEXT I
136 IF Z<Y+1 OR Z>Y+2 THEN RETURN
137 CALL SPEECH("L,"&EXP$)
138 FOR I=1 TO 5
139 CALL COLOR("0RR")
140 LOCATE (X,Y+1):PRINT "    "
141 LOCATE (X+1,Y+1):PRINT "    "
142 CALL COLOR("0YY")
143 LOCATE (X,Y+1):PRINT "    "
144 LOCATE (X+1,Y+1):PRINT "    "
145 CALL COLOR("0CC")
146 LOCATE (X,Y+1):PRINT "    "
147 LOCATE (X+1,Y+1):PRINT "    "
148 NEXT I
149 PAUSE .3
150 S=S-1
151 IF S=0 THEN GOTO 153
152 GOTO 43
153 CALL SPEECH("A,"&BAT$)
154 FOR A=1 TO 2
155 CALL SPEECH("L,"&CAR$)
156 PAUSE 2
157 NEXT A
158 PAUSE 1
159 GOTO 42
160 !
161 ! REGLES DE JEU
162 !
163 CLS "YYY"
164 DIM A$(17)
165 A$(2)="               PAR JOCELYN DONZE"
166 A$(4)="DEFENSEUR D'UNE STATION SPATIALE,VOUS"
167 A$(6)="ETES ASSAILLI PAR DES HORDES"
168 A$(8)="D'ENVAHISSEURS,CEUX CI DEVENANT DE"
169 A$(10)="PLUS EN PLUS AGRESSIFS..."
170 A$(12)="SAUREZ VOUS DEFENDRE LA TERRE,TERRIEN ?"
171 A$(15)=" TOUCHES:"
172 A$(16)="  DEPLACEMENT:CURSEUR: <- ET ->"
173 A$(17)="  FEU:BARRE D'ESPACEMENT"
174 CALL COLOR("0RHLF")
175 LOCATE (1,11):PRINT "EEXXEELLBBLLIITTZZ"
176 LOCATE (2,11):PRINT "EEXXEELLBBLLIITTZZ"
177 CALL COLOR("0RY")
178 LOCATE (4,1)
179 PRINT "VOULEZ-VOUS LES REGLES ?"
180 KE$=KEY$
181 IF KE$="N" THEN RETURN
182 FOR I=1 TO 17
183 FOR J=1 TO LEN(A$(I))
184 CALL COLOR("0RR")
185 LOCATE (I+2,J):PRINT " "
186 CALL COLOR("0BY")
187 LOCATE (I+2,J):PRINT SEG$(A$(I),J,1)
188 NEXT J
189 NEXT I
190 CALL COLOR("0MF"):LOCATE (22,12)
191 PRINT "APPUYER POUR JOUER"
192 B$=KEY$
193 RANDOMIZE
194 RETURN
