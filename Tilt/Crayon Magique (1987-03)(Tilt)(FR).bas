100 !          ******************
105 !          * CRAYON MAGIQUE *
110 !          * pour exelvision*
115 !          *    EXL 100     *
120 !          * TILT/ CHAGNY J *
125 !          ******************
130 GOSUB 610:!envoi a la presentation
135 PF=-2
140 CALL CHAR(1,"007E7E7E7E7E7E7E7E00")
145 !          ******************
150 !          * DESSIN TABLEAU *
155 !          ******************
160 CLS "Bbb"
165 C$="RGMCYbWB"
170 X=160:Y=100:D$="C":XF=0
175 CALL HRON("B",1,19)
180 FOR I=3 TO 38 STEP 5
185 IF I=3 THEN J$="R"
190 IF I=8 THEN J$="G"
195 IF I=13 THEN J$="M"
200 IF I=18 THEN J$="C"
205 IF I=23 THEN J$="Y"
210 IF I=28 THEN J$="b"
215 IF I=33 THEN J$="W"
220 IF I=38 THEN J$="B"
225 CALL COLOR("1"&J$&"B")
230 LOCATE (20,I):PRINT CHR$(1)
235 NEXT I
240 !          ******************
245 !          *    PROGRAMME   *
250 !          ******************
255 CALL PLOT(D$,X,Y)
260 CALL KEY1(XE,TE):A$=CHR$(XE):IF TE=0 THEN 260
265 IF XE=42 THEN GOSUB 435:GOSUB 470:GOSUB 380:GOTO 255
270 IF XE=35 THEN GOSUB 360:GOTO 260
275 IF XE=32 THEN CALL SPEECH("L,"&"0E683FC7031EFBAB602FC"):GOTO 160
280 IF XE<49 OR XE>57 THEN 260
285 IF A$="8"THEN A$="6":GOTO 315
290 IF A$="4"THEN A$="8":GOTO 315
295 IF A$="6"THEN A$="4":GOTO 315
300 IF A$="9"THEN A$="1":GOTO 315
305 IF A$="1"THEN A$="5":GOTO 315
310 IF A$="5"THEN A$="6"
315 F=-2*(VAL(A$)<5)-1
320 A=X:B=Y
325 U=VAL(A$)+2*(F-1)
330 ON U GOSUB 395,415,410,400
335 D=-(X>0 AND X<319 AND Y>0 AND Y<189)
340 IF D=0 THEN CALL SPEECH("L,"&"0D7C4FEA2BAB602FC"):GOSUB 425
345 IF S=35 THEN CALL PLOT("B",A,B)
350 CALL SPEECH("L,"&"0D7C02FC")
355 GOTO 255
360 CALL SPEECH("L,"&"0D7CFFFFBAB602FC")
365 PAUSE .4
370 S=35:SS=SS+1:IF SS>1 THEN SS=0:S=0:RETURN
375 RETURN
380 IF XF>8 THEN XF=1
385 D$=SEG$(C$,XF,1)
390 RETURN
395 Y=Y+F
400 X=X+F
405 RETURN
410 GOSUB 400
415 Y=Y-F
420 RETURN
425 X=A:Y=B
430 RETURN
435 !          ******************
440 !          * POINTEUR CHOIX *
445 !          * DES 8 COULEURS *
450 !          ******************
455 CALL SPEECH("L,"&"07DC5372BAB602FC")
460 XF=XF+1:IF XF>8 THEN XF=1:RETURN
465 RETURN
470 !          ******************
475 !          * SELECTION SENS *
480 !          * ET COULEURS.   *
485 !          ******************
490 FOR I=3 TO 38 STEP 5
495 IF I=3 THEN J$="R"
500 IF I=8 THEN J$="G"
505 IF I=13 THEN J$="M"
510 IF I=18 THEN J$="C"
515 IF I=23 THEN J$="Y"
520 IF I=28 THEN J$="b"
525 IF I=33 THEN J$="W"
530 IF I=38 THEN J$="B"
535 LOCATE (20,I)
540 CALL COLOR("1"&J$&"B")
545 PRINT CHR$(1)
550 NEXT I
555 IF XF=1 THEN I=3:J$="R":W$="B"
560 IF XF=2 THEN I=8:J$="G":W$="B"
565 IF XF=3 THEN I=13:J$="M":W$="B"
570 IF XF=4 THEN I=18:J$="C":W$="B"
575 IF XF=5 THEN I=23:J$="Y":W$="B"
580 IF XF=6 THEN I=28:J$="b":W$="B"
585 IF XF=7 THEN I=33:J$="W":W$="B"
590 IF XF=8 THEN I=38:J$="B":W$="W"
595 CALL COLOR("1"&W$&J$&"F")
600 LOCATE (20,I):PRINT CHR$(1)
605 RETURN
610 !          ******************
615 !          *  PRESENTATION  *
620 !          ******************
625 CLS "BCb"
630 A=1:GOSUB 655
635 B=1:GOSUB 710
640 C=1:GOSUB 710
645 D=1:GOSUB 710
650 RETURN
655 !          ******************
660 !          * DESSIN MANETTE *
665 !          ******************
670 CALL COLOR("1BY")
675 LOCATE (18,5):PRINT "*EXL*  CRAYON  MAGIQUE  *EXL*"
680 CALL COLOR("1YB")
685 LOCATE (20,5):PRINT "*100*  TILT / CHAGNY J  *100*"
690 FOR I=4 TO 15
695 CALL COLOR("1BB")
700 LOCATE (I,16):PRINT "       "
705 NEXT I
710 FOR I=8 TO 14 STEP 2
715 CALL COLOR("1BY")
720 FOR J=17 TO 21 STEP 2
725 S=S+1:S$=STR$(S)
730 IF C=1 AND S=12 THEN CALL COLOR("1WRF")
735 IF S=10 THEN S$="*"
740 IF S=11 THEN S$="0"
745 IF S=12 THEN S$="#"
750 IF B=1 THEN CALL COLOR("1WRF")
755 IF B=1 AND S>9 THEN CALL COLOR("1BY")
760 IF B=1 AND S=5 THEN CALL COLOR("1BY")
765 IF D=1 AND S=10 THEN CALL COLOR("1WRF")
770 LOCATE (I,J):PRINT S$
775 IF B=1 THEN CALL COLOR("1WRF")
780 IF D=1 AND S=10 THEN CALL COLOR("1BY")
785 NEXT J
790 NEXT I
795 IF A=1 THEN CALL COLOR("1WRF")
800 IF C=1 THEN CALL COLOR("1BY")
805 IF B=1 THEN CALL COLOR("1BY")
810 LOCATE (5,16):PRINT "! "
815 LOCATE (5,21):PRINT " !"
820 IF B=1 THEN 840
825 IF C=1 THEN 845
830 IF D=1 THEN 850
835 LOCATE (2,12):PRINT "* EFFACEMENT *":PAUSE 10:A=0:S=0
840 IF B=1 THEN LOCATE (2,11):PRINT "* ORIENTATIONS *":PAUSE 9:B=0:S=0
845 IF C=1 THEN LOCATE (2,11):PRINT "* DEPLACEMENTS *":PAUSE 9:C=0:S=0
850 IF D=1 THEN LOCATE (2,11):PRINT "* COULEURS --> *":PAUSE 9:D=0:S=0
855 CALL COLOR("1BY"):RETURN
860 !          ******************
865 !          * FIN DU LISTING *
870 !          ******************
