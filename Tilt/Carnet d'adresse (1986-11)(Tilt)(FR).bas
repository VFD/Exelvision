
10 !**************************************
20 !*        CARNET D'ADRESSES         ***
30 !*           Pour exl 100           ***
40 !*        -C- TILT & CHAGNY         ***
50 !**************************************
60 !
70 !*GESTION DE FICHIER POUR MAGNETOPHONE*
75 !*POUR AUGMENTER LE NOMBRE DE FICHES  *
76 !*MODIFIEZ le variable NF=5 ligne 95  *
77 !*    MINIMUN NF=1 MAXIMUM=NF=99      *
80 !
90 !*********DECLARATIONS*****************
95 NF=5!Nombre de fiches autorisees
100 DIM N$(99),P$(99),A$(99),C$(99),V$(99),T$(99),S$(99),O$(99),M$(99)
110 CLS "YBB"
120 GOSUB 1400:GOSUB 1470:GOSUB 1550:GOSUB 1650:GOSUB 1690
130 CALL KEY1(A,B):IF B=0 THEN 130
140 IF A=76 OR A=108 THEN GOSUB 1910
150 IF A=83 OR A=115 THEN GOSUB 1800
160 IF A=67 OR A=99 THEN GOSUB 260
170 IF A=82 OR A=114 THEN GOSUB 450
180 IF A=130 THEN GOSUB 670
190 IF A=128 THEN GOSUB 720
200 IF A=77 OR A=109 THEN GOSUB 790
210 IF A=69 OR A=101 THEN GOSUB 1180
220 IF A=72 OR A=104 THEN GOSUB 980:GOTO 110
230 IF A=27 THEN GOSUB 1270
240 CALL SPEECH("L,"&"0D7C9999BAB602FC"):GOTO 130
250 !
255 !*********CREATION FICHE***************
260 GOSUB 1300:CALL COLOR("0BW")
270 LOCATE (22,12):PRINT "Cr"&CHR$(17)&CHR$(17)
280 FOR I=1 TO 99
290 IF N$(I)="" THEN N=I:I=99
300 NEXT I
310 CALL COLOR("0WM")
320 LOCATE (8,8):ACCEPT VALIDATE(UALPHA)SIZE(11),N$(N)
330 LOCATE (8,27):ACCEPT VALIDATE(UALPHA)SIZE(11),P$(N)
340 LOCATE (10,12):ACCEPT VALIDATE(ALPHANUM)SIZE(26),A$(N)
350 LOCATE (12,11):ACCEPT VALIDATE(DIGIT)SIZE(5),C$(N)
360 LOCATE (12,23):ACCEPT VALIDATE(UALPHA)SIZE(15),V$(N)
370 LOCATE (14,14):ACCEPT VALIDATE(NUMERIC)SIZE(11),T$(N)
380 LOCATE (14,33):ACCEPT VALIDATE(DIGIT)SIZE(6),S$(N)
390 LOCATE (16,16):ACCEPT SIZE(22),O$(N)
400 LOCATE (18,16):ACCEPT SIZE(-22),M$(N)
410 CALL COLOR("0YR")
420 LOCATE (22,12):PRINT "Cr"&CHR$(17)&CHR$(17)
430 RETURN
440 !
445 !*********RECHERCHE UNE FICHE**********
450 CALL COLOR("0BW")
460 LOCATE (22,17):PRINT "Rech"
470 GOSUB 1300
480 CALL COLOR("0WM")
490 LOCATE (8,8):ACCEPT VALIDATE(UALPHA)SIZE(11),R$
500 FOR I=1 TO 99
510 IF N$(I)=R$ THEN N=I:I=99
520 NEXT I
530 CALL COLOR("0WM"):LOCATE (8,8):PRINT N$(N)
540 LOCATE (8,8):PRINT N$(N)
550 LOCATE (8,27):PRINT P$(N)
560 LOCATE (10,12):PRINT A$(N)
570 LOCATE (12,11):PRINT C$(N)
580 LOCATE (12,23):PRINT V$(N)
590 LOCATE (14,14):PRINT T$(N)
600 LOCATE (14,33):PRINT S$(N)
610 LOCATE (16,16):PRINT O$(N)
620 LOCATE (18,16):PRINT M$(N)
630 CALL COLOR("0YR")
640 LOCATE (22,17):PRINT "Rech"
650 RETURN
660 !
665 !*********FICHE SUIVANTE***************
670 CALL COLOR("0BW"):LOCATE (22,22):PRINT "^"&" "&CHR$(14)
680 N=N+1:IF N>99 THEN N=1
690 GOSUB 1300:GOSUB 530
700 CALL COLOR("0YR"):LOCATE (22,22):PRINT "^"&" "&CHR$(14)
710 RETURN
720 !
725 !*********FICHE PRECEDENTE*************
730 CALL COLOR("0BW"):LOCATE (22,22):PRINT "^"&" "&CHR$(14)
740 N=N-1:IF N<1 THEN N=1
750 GOSUB 1300:GOSUB 530
760 CALL COLOR("0YR"):LOCATE (22,22):PRINT "^"&" "&CHR$(14)
770 RETURN
780 !
790 !*********MODIFICATION FICHE***********
800 CALL COLOR("0BW")
810 LOCATE (22,26):PRINT "Modi"
820 GOSUB 470
840 CALL COLOR("0WM")
850 LOCATE (8,8):ACCEPT VALIDATE(UALPHA)SIZE(-11),N$(N)
860 LOCATE (8,27):ACCEPT VALIDATE(UALPHA)SIZE(-11),P$(N)
870 LOCATE (10,12):ACCEPT VALIDATE(ALPHANUM)SIZE(-26),A$(N)
880 LOCATE (12,11):ACCEPT VALIDATE(DIGIT)SIZE(-5),C$(N)
890 LOCATE (12,23):ACCEPT VALIDATE(UALPHA)SIZE(-15),V$(N)
900 LOCATE (14,14):ACCEPT VALIDATE(NUMERIC)SIZE(-11),T$(N)
910 LOCATE (14,33):ACCEPT VALIDATE(DIGIT)SIZE(-6),S$(N)
920 LOCATE (16,16):ACCEPT SIZE(-22),O$(N)
930 LOCATE (18,16):ACCEPT SIZE(-22),M$(N)
940 CALL COLOR("0YR")
950 LOCATE (22,26):PRINT "Modi"
960 RETURN
970 !
975 !*********HELP UTILISATION**************
980 CALL COLOR("0BW")
990 LOCATE (22,36):PRINT "Help"
1000 CLS:CALL COLOR("0Yb"):GOSUB 1480
1010 CALL COLOR("0YBHL"):LOCATE (3,9):PRINT "UUTTIILLIISSAATTIIOONN"
1020 CALL COLOR("0YBHL"):LOCATE (4,9):PRINT "UUTTIILLIISSAATTIIOONN"
1030 CALL COLOR("0Cb"):LOCATE (8,4):PRINT "[L]oad pour charger le fichier"
1040 LOCATE (9,4):PRINT "a partir du magnetophone.     "
1050 LOCATE (10,4):PRINT "Suivre les instructions RADIAN."
1060 LOCATE (11,4):PRINT "[S]ave pour sauvegarder sur le "
1070 LOCATE (12,4):PRINT "magnetophone. Suivre RADIAN    "
1080 LOCATE (13,4):PRINT "[C]ree un enregistrement nouveau."
1090 LOCATE (14,4):PRINT "[R]echercher un enregistrement.  "
1100 LOCATE (15,4):PRINT "[FLECHES] fiche suivante/precedente"
1110 LOCATE (16,4):PRINT "[M]odifier un enregistrement.     "
1120 LOCATE (17,4):PRINT "[E]ffacer un enregistrement + [E] "
1130 LOCATE (18,4):PRINT "[ESC]ape pour sortir du logiciel. "
1140 LOCATE (22,4):PRINT "Pressez [ENTER] pour continuer... "
1145 CALL COLOR("0YB"):LOCATE (7,4):PRINT "Ligne 95 NF=Nbr fiches (MAXI=99)"
1150 PAUSE
1160 RETURN
1170 !
1175 !********DETRUIRE UNE FICHE************
1180 CALL COLOR("0BW")
1190 LOCATE (22,31):PRINT "Eras"
1200 GOSUB 470
1210 CALL KEY1(A,B):IF B=0 THEN 1210
1220 IF A=69 THEN 1230 ELSE 1240
1230 P$(N),A$(N),C$(N),V$(N),T$(N),S$(N),O$(N),M$(N)="":N$(N)="  VIERGE   "
1240 CALL COLOR("0YR")
1250 LOCATE (22,31):PRINT "Eras"
1260 RETURN
1270 !
1275 !***********FIN RETOUR BASIC***********
1280 CLS:END
1290 !
1295 !***********EFFACE MASQUE**************
1300 CALL COLOR("0MM"):LOCATE (8,8):PRINT RPT$(" ",11)
1310 LOCATE (8,27):PRINT RPT$(" ",11)
1320 LOCATE (10,12):PRINT RPT$(" ",26)
1330 LOCATE (12,11):PRINT RPT$(" ",5)
1340 LOCATE (12,23):PRINT RPT$(" ",15)
1350 LOCATE (14,14):PRINT RPT$(" ",11)
1360 LOCATE (14,33):PRINT RPT$(" ",6)
1370 LOCATE (16,16):PRINT RPT$(" ",22)
1380 LOCATE (18,16):PRINT RPT$(" ",22)
1390 RETURN
1400 !
1405 !***********DECLARATIONS***************
1410 HL$=CHR$(9)&RPT$(CHR$(4),38)&CHR$(6)
1420 BL$=CHR$(8)&RPT$(CHR$(4),38)&CHR$(7)
1430 ML$=CHR$(3)&RPT$(CHR$(32),38)&CHR$(3)
1440 IL$=CHR$(25)&RPT$(CHR$(4),38)&CHR$(23)
1450 RETURN
1460 !
1465 !************MASQUE********************
1470 CALL COLOR("0YM")
1480 LOCATE (1,1):PRINT HL$
1490 FOR I=2 TO 19
1500 LOCATE (I,1):PRINT ML$
1510 NEXT I
1520 LOCATE (20,1):PRINT BL$
1530 LOCATE (6,1):PRINT IL$
1540 RETURN
1545 !
1546 !***********MASQUE CHOIX***************
1550 CALL COLOR("0YR")
1560 LOCATE (22,2):PRINT "Load"
1570 LOCATE (22,7):PRINT "Save"
1580 LOCATE (22,12):PRINT "Cr"&CHR$(17)&CHR$(17)
1590 LOCATE (22,17):PRINT "Rech"
1600 LOCATE (22,22):PRINT "^"&" "&CHR$(14)
1610 LOCATE (22,26):PRINT "Modi"
1620 LOCATE (22,31):PRINT "Eras"
1630 LOCATE (22,36):PRINT "Help"
1640 RETURN
1645 !
1646 !************TITRE*********************
1650 CALL COLOR("0WMHL")
1660 LOCATE (3,4):PRINT "CCAARRNNEETT  DD''AADDRREESSSSEESS"
1670 LOCATE (4,4):PRINT "CCAARRNNEETT  DD''AADDRREESSSSEESS"
1680 RETURN
1685 !
1686 !*******AFFICHAGE RUBRIQUES************
1690 CALL COLOR("0YM"):LOCATE (8,4):PRINT "Nom:"
1700 LOCATE (8,20):PRINT "Pr"&CHR$(17)&"nom:"
1710 LOCATE (10,4):PRINT "Adresse:"
1720 LOCATE (12,4):PRINT "C/Post:"
1730 LOCATE (12,17):PRINT "Ville:"
1740 LOCATE (14,4):PRINT "T"&CHR$(17)&"l"&CHR$(17)&"phone:"
1750 LOCATE (14,26):PRINT "Poste:"
1760 LOCATE (16,4):PRINT "Observation:"
1770 LOCATE (18,4):PRINT "Commentaire:"
1780 RETURN
1790 !
1800 !
1805 !*********SAUVEGARDE DU FICHIER********
1810 CALL COLOR("0BW"):LOCATE (22,7):PRINT "Save"
1820 OPEN #1,"1.CARN",OUTPUT,VARIABLE 129
1830 FOR I=1 TO NF
1840 PRINT #1,N$(I);",";P$(I);",";A$(I);",";C$(I);",";V$(I);",";T$(I)
1850 PRINT #1,S$(I);",";O$(O);",";M$(I)
1860 NEXT I
1870 CLOSE #1
1880 CALL COLOR("0YR"):LOCATE (22,7):PRINT "Save"
1890 RETURN
1900 !
1905 !**********CHARGEMENT DU FICHIER*******
1910 CALL COLOR("0BW"):LOCATE (22,2):PRINT "Load"
1920 OPEN #1,"1.CARN",INPUT,VARIABLE 129
1930 FOR I=1 TO NF
1940 INPUT #1,N$(I),P$(I),A$(I),C$(I),V$(I),T$(I)
1950 INPUT #1,S$(I),O$(I),M$(I)
1960 NEXT I
1970 CLOSE #1
1980 CALL COLOR("0YR"):LOCATE (22,2):PRINT "Load"
1990 RETURN
2000 !**************************************
2010 !*     FIN DU PROGRAMME               *
2020 !**************************************
