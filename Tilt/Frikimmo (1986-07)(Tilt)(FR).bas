
1 !                         ************
2 !                         * FRIKIMMO *
3 !                         * TILT &   *
4 !                         * CHAGNY J *
5 !                         ************
6 !
7 FL1$="83AF5139282726BE4452A37EE5F8ECD14DEE75E36B540E8959856F99C5D11F3ABE4654"
8 FL2$="C7FEA2F8E2C05DEEB3E2B38574DD378CCF16D26D3F333E5B88D4F9F2FF"
9 FLI$=FL1$&FL2$
10 BU$="28D5AA4C35B39094B932956E5CC2E4D704AB72D242D9008B4881F71398D59326DA85"
11 O$="2A6C12AD0D0D2F254EB4D14C6A5819C93496AE506C6525AB2E5A19AEB5AC64D995B3D2"
12 N$="B1949424AB5385DB31922DCE9EA94AC989BAB602FCFFFF"
13 J$="56B14C92A89806D5CA59A1334F75CB340A28505D01DB76257DDB1B53959CC4C05323"
14 CALL POKE(50920,162,5,45,162,136,45,10):CALL EXEC(50920)
15 CLS "WBB":CALL COLOR("0YBL")
16 FOR I=6 TO 19
17 CALL COLOR("0YBL"):IF (I/2)-INT(I/2)=0 THEN CALL COLOR("0RBL")
18 LOCATE (I,(I-5)*2):PRINT "FFRRIICCKKIIMMMMOO"
19 CALL SPEECH("L,"&"0D7CCB37BAB602FC")
20 NEXT I
21 FOR I=1 TO 21:PRINT:PAUSE .1:CALL SPEECH("L,"&"0D7C5E5EBAB602FC"):NEXT I
22 L$="Tilt"
23 CALL COLOR("0RB")
24 FOR I=1 TO 21
25 CALL SPEECH("L,"&"0D7C4321BAB602FC")
26 LOCATE (I,1):PRINT RPT$(L$,10)
27 NEXT I
28 CALL COLOR("1YBHL"):LOCATE (4,4):PRINT "FFRRIICCKKIIMMMMOO"
29 LOCATE (5,4):PRINT "FFRRIICCKKIIMMMMOO"
30 CALL COLOR("1RBHLF")
31 CALL SPEECH("L,"&BU$&J$&O$&N$):PAUSE .4:LOCATE (6,8):PRINT "TTIILLTT"
32 LOCATE (7,8):PRINT "TTIILLTT"
33 CALL COLOR("0YB"):DE$="APPUYER SUR UNE TOUCHE POUR LA SUITE.  "
34 CALL COLOR("0YB"):LOCATE (21,1):DS$=SEG$(DE$,1,38):PRINT " ";DS$;" ":RD=RD+1
35 DE$=SEG$(DE$,2,40)&SEG$(DE$,1,1):PAUSE .04:CALL KEY1(A,B):IF B<>0 THEN 43
36 GOSUB 39
37 GOTO 34
38 CALL COLOR("0"&S1$):LOCATE (L,4):PRINT "FFRRIICCKKIIMMMMOO":RETURN
39 S1$="bBHL":L=4:GOSUB 38:S1$="bBHL":L=5:GOSUB 38:S1$="MBHL":L=4
40 GOSUB 38:S1$="MBHL":L=5:GOSUB 38:S1$="GBHL":L=4:GOSUB 38:S1$="GBHL"
41 L=5:GOSUB 38:S1$="YBHL":L=4:GOSUB 38:S1$="YBHL":L=5:GOSUB 38
42 S1$="CBHL":L=4:GOSUB 38:S1$="CBHL":L=5:GOSUB 38:RETURN
43 CLS "RBG":CALL COLOR("0GGHLF"):LOCATE (10,6):PRINT "FFRRIICCKKIIMMMMOO"
44 LOCATE (11,6):PRINT "FFRRIICCKKIIMMMMOO":CALL COLOR("0GB"):LOCATE (1,1)
45 PRINT "-C- TILT & CHAGNY jean"&RPT$(" ",17)
46 LOCATE (13,5):CALL COLOR("0GB"):PRINT "Version C/1.00"
47 DIM T(40,17),T$(40),CJ(2),W(2),H1$(6),H2$(6),CUC$(2),CUF$(2),PLU1$(7)
48 DIM PLU2$(7),PLU(7),MIN1$(7),MIN2$(7),MIN(7),DEP1$(7),DEP2$(7),DER(7),PR(2)
49 T(W(J),15)=11:T(W(J),16)=2:PR(1)=0:PR(2)=0:T(W(J),2)=66:CJ(2)=150000
50 CJ(1)=150000
51 DATA 0,66,98,49,0,5000,6000,200,1000,3000,9000,16000,250000,12,10,3,6
52 DATA 3,98,71,0,0,0,0,0,0,0,0,0,0,0,9,3,21
53 DATA 0,66,98,50,0,5000,6000,400,2000,6000,18000,32000,45000,13,8,3,6
54 DATA 4,82,66,0,0,0,0,0,0,0,-20000,0,0,0,7,3,73
55 DATA 0,66,87,77,0,0,20000,2500,0,0,0,0,0,34,6,3,13
56 DATA 0,98,87,49,0,5000,10000,600,3000,9000,27000,40000,50000,14,5,3,12
57 DATA 3,77,71,0,0,0,0,0,0,0,0,0,0,0,4,3,63
58 DATA 0,98,87,50,0,5000,10000,600,3000,9000,27000,40000,50000,15,3,3,12
59 DATA 0,98,87,51,0,5000,12000,800,4000,10000,30000,45000,60000,16,2,3,12
60 DATA 5,71,71,0,0,0,0,0,0,0,0,0,0,0,1,3,19
61 DATA 0,77,87,49,0,10000,14000,1000,5000,15000,45000,62500,75000,17,1,4,8
62 DATA 0,89,66,69,0,0,15000,400,0,0,0,0,0,38,1,5,20
63 DATA 0,77,87,50,0,10000,14000,1000,5000,15000,45000,62500,75000,18,1,6,8
64 DATA 0,77,87,51,0,10000,16000,1200,6000,18000,50000,70000,90000,19,1,7,8
65 DATA 0,66,87,76,0,0,20000,2500,0,0,0,0,0,35,1,8,13
66 DATA 0,82,89,49,0,10000,18000,1400,7000,20000,55000,75000,95000,20,1,9,7
67 DATA 3,98,71,0,0,0,0,0,0,0,0,0,0,0,1,10,21
68 DATA 0,82,89,50,0,10000,18000,1400,7000,20000,55000,75000,95000,21,1,11,7
69 DATA 0,82,89,51,0,10000,20000,1600,8000,22000,60000,80000,100000,22,1,12,7
70 DATA 6,82,71,0,0,0,0,0,0,0,0,0,0,0,1,13,71
71 DATA 0,82,66,49,0,15000,22000,1800,9000,25000,70000,87500,105000,23,2,13,9
72 DATA 3,77,71,0,0,0,0,0,0,0,0,0,0,0,3,13,63
73 DATA 0,82,66,50,0,15000,22000,1800,9000,25000,70000,87500,105000,24,4,13,9
74 DATA 0,82,66,49,0,15000,24000,2000,10000,30000,75000,92500,110000,25,5,13,9
75 DATA 0,66,87,78,0,0,20000,2500,0,0,0,0,0,36,6,13,13
76 DATA 0,89,66,49,0,15000,26000,2200,11000,33000,80000,97500,115000,26,7,13,12
77 DATA 0,89,66,50,0,15000,26000,2200,11000,33000,80000,97500,115000,27,8,13,12
78 DATA 0,87,98,67,0,0,15000,400,0,0,0,0,0,39,9,13,14
79 DATA 0,89,66,51,0,15000,28000,2400,12000,36000,85000,102500,120000,28,10,13
80 DATA 12,7,66,98,0,0,0,0,0,0,0,0,0,0,0,11,13,80,0
81 DATA 66,71,49,0,20000,30000,2600,13000,39000,90000,110500,127500,29,11,12,10
82 DATA 0,66,71,50,0,20000,30000,2600,13000,39000,90000,110500,127500,30,11,11
83 DATA 10,3,98,71,0,0,0,0,0,0,0,0,0,0,0,11,10,21
84 DATA 0,66,71,51,0,20000,32000,2800,15000,45000,100000,120000,140000,31,11,9
85 DATA 10,0,66,87,83,0,0,20000,2500,0,0,0,0,0,37,11,8,13
86 DATA 3,77,71,0,0,0,0,0,0,0,0,0,0,0,11,7,63
87 DATA 0,98,89,49,0,20000,35000,3500,17500,50000,110000,130000,150000,32,11,6
88 DATA 11,4,82,66,0,0,0,0,0,0,0,-10000,0,0,0,11,5,84
89 DATA 0,98,89,50,0,20000,40000,5000,20000,60000,140000,170000,200000,33,11,4
90 DATA 11,8,82,87,0,0,0,0,20000,0,0,0,0,0,0,11,3,68
91 DATA " BELLEVILLE "," COMMUNAUTE ","  LECOURBE  "," IMPOSITION "
92 DATA "MONTPARNASSE"," VAUGIRARD ","   CHANCE   "," COURCELLES "
93 DATA " REPUBLIQUE ","   VISITE   ","LA VILLETTE ","ELECTRICITE "
94 DATA "  NEUILLY   ","  PARADIS   ","    LYON    ","   MOZART   "
95 DATA " COMMUNAUTE ","SAINT-MICHEL","  PIGALLE   ","PARC GRATUIT"
96 DATA "  MATIGNON  ","   CHANCE   ","MALESHERBES ","HENRI-MARTIN"
97 DATA "GARE DU NORD","SAINT-HONORE"," LA BOURSE  ","CIE DES EAUX"
98 DATA " LA FAYETTE ","-> EN PRISON","  BRETEUIL  ","  AV. FOCH  "
99 DATA " COMMUNAUTE "," CAPUCINES  ","SAINT-LAZARE","   CHANCE   "
100 DATA "CH. ELYSEES ","TAXE DE LUXE","R.DE LA PAIX","CASE DEPART "
101 FOR I=1 TO 40:FOR J=1 TO 17:READ T(I,J):NEXT J:NEXT I
102 FOR I=1 TO 40:READ T$(I):NEXT I:RESTORE 447
103 FOR I=1 TO 7:READ PLU1$(I),PLU2$(I),PLU(I):NEXT I
104 FOR I=1 TO 7:READ MIN1$(I),MIN2$(I),MIN(I):NEXT I
105 FOR I=1 TO 7:READ DEP1$(I),DEP2$(I),DER(I):NEXT I
106 J=2
107 RANDOMIZE RD
108 E$=CHR$(20):G$=CHR$(14)
109 CALL CHAR(3,"01010101010101010101"):CALL CHAR(4,"000000000000000000FF")
110 CALL CHAR(5,"81818181818181818181"):CALL CHAR(6,"AA55AA55AA55AA55AA55")
111 CALL CHAR(7,"8181818181AA55AA55AA"):CALL CHAR(8,"8181818181FFFFFFFFFF")
112 CALL CHAR(9,"FFFFFFFFFFFFFFFFFFFF"):CALL CHAR(10,"AA55AA55AA8181818181")
113 CALL CHAR(11,"FFFFFFFFFF8181818181"):CALL CHAR(12,"AA55AA55AA55AA55AA55")
114 CALL CHAR(13,"000023247C24000000FF"):CALL CHAR(14,"003E88FEFE8606060000")
115 CALL CHAR(18,"000000FFFFFFFFFFFFFF"):CALL CHAR(19,"0000001F1F1F1F1F1F1F")
116 CALL CHAR(20,"0018183C7E7E7E3C1800"):CALL CHAR(21,"000000003C3C3C000000")
117 CALL CHAR(31,"0018183C5A9918244281"):CALL CHAR(22,"00000000181800000000")
118 CALL CHAR(23,"00606000000000060600"):CALL CHAR(24,"00606000181800060600")
119 CALL CHAR(25,"00666600000000666600"):CALL CHAR(26,"00666600181800666600")
120 CALL CHAR(27,"00666600666600666600"):CALL CHAR(33,"FF80BFA0A0B8A0A0A080")
121 CALL CHAR(34,"80808080808080808080"):CALL CHAR(35,"808080808080808080FF")
122 CALL CHAR(36,"FF000000000000000000"):CALL CHAR(37,"000000000000000000FF")
123 CALL CHAR(38,"FF010101010101010101"):CALL CHAR(39,"01010101010101010101")
124 CALL CHAR(40,"013945454545453901FF"):CALL CHAR(41,"C3C3C3C3FFFFC3C3C3C3")
125 BON$=CHR$(31)
126 H1$(1)=CHR$(22):H1$(2)=CHR$(23):H1$(3)=CHR$(24):H1$(4)=CHR$(25)
127 CUC$(1)="R":CUC$(2)="b":CUF$(1)="B":CUF$(2)="W"
128 M1$(1)=CHR$(22):M1$(2)=CHR$(23):M1$(3)=CHR$(24):M1$(4)=CHR$(41)
129 M1$(5)=CHR$(26)
130 H1$(5)=CHR$(26):H1$(6)=CHR$(27)
131 H2$(1)=CHR$(22):H2$(2)=CHR$(23):H2$(3)=CHR$(24):H2$(4)=CHR$(25)
132 H2$(5)=CHR$(26):H2$(6)=CHR$(27)
133 CALL COLOR("0RRL"):LOCATE (21,6)
134 PRINT "MMAANNEETTTTEESS  ''OO//NN''":PGN$=KEY$:IF PGN$="O" THEN PGN=1
135 IF PGN$<>"O"AND PGN$<>"N" THEN 133
136 CLS "WBB":CALL COLOR("1GG")
137 FOR I=2 TO 10:LOCATE (I,4):PRINT RPT$(" ",9):NEXT I
138 CALL COLOR("1BG"):LOCATE (2,12):PRINT CHR$(38):LOCATE (10,4):PRINT CHR$(35)
139 LOCATE (2,4):PRINT CHR$(33)
140 LOCATE (3,5):PRINT "R"
141 LOCATE (4,6):PRINT "I"
142 LOCATE (5,7):PRINT "C"
143 LOCATE (6,8):PRINT "K"
144 LOCATE (7,9):PRINT "I"
145 LOCATE (8,10):PRINT "M"
146 LOCATE (9,11):PRINT "M"
147 LOCATE (10,12):PRINT CHR$(40)
148 GOSUB 265
149 CALL COLOR("1MM")
150 FOR I=13 TO 15:LOCATE (I,12):PRINT RPT$(" ",28):NEXT I
151 FOR I=17 TO 19:LOCATE (I,12):PRINT RPT$(" ",28):NEXT I
152 CALL COLOR("1BG")
153 FOR I=4 TO 12:LOCATE (1,I):PRINT CHR$(3):LOCATE (11,I):PRINT CHR$(3)
154 NEXT I
155 FOR I=1 TO 11:LOCATE (I,2):PRINT CHR$(4)&CHR$(4):LOCATE (I,13)
156 PRINT CHR$(4)&CHR$(4):NEXT I
157 FOR I=5 TO 11:LOCATE (2,I):PRINT CHR$(36):LOCATE (10,I):PRINT CHR$(37)
158 NEXT I
159 FOR I=3 TO 9:LOCATE (I,4):PRINT CHR$(34):LOCATE (I,12):PRINT CHR$(39)
160 NEXT I
161 FOR I=1 TO 40:CALL COLOR("1"&CHR$(T(I,2))&"G"):LOCATE (T(I,15),T(I,16))
162 PRINT CHR$(T(I,17)):NEXT I
163 J=-J+3
164 IF PR(2)=0 AND PR(1)=0 THEN CALL COLOR("0RR"):LOCATE (1,3):PRINT " "
165 IF PR(J)<>0 THEN PR(J)=PR(J)-1
166 IF PR(J)<>0 THEN J=(-J+3)
167 IF DOUB=2 THEN J=-J+3:DOUB=0
168 CALL COLOR("0bC")
169 FOR I=1 TO 11:LOCATE (I,16):PRINT RPT$(" ",12):NEXT I
170 CALL COLOR("0RR")
171 FOR I=13 TO 15:LOCATE (I,2):PRINT RPT$(" ",9):NEXT I
172 CALL COLOR("0bb")
173 FOR I=17 TO 19:LOCATE (I,2):PRINT RPT$(" ",9):NEXT I
174 CALL COLOR("0YY")
175 FOR I=1 TO 5:LOCATE (I,29):PRINT RPT$(" ",11):NEXT I
176 FOR I=7 TO 11:LOCATE (I,29):PRINT RPT$(" ",11):NEXT I
177 IF CJ(1)<0 OR CJ(2)<0 THEN CLS "BGG":CALL COLOR("0BG")
178 IF CJ(1)<0 THEN CLS "Wbb":LOCATE (12,10):PRINT "Faillite du JOUEUR 1":RUN
179 IF CJ(2)<0 THEN CLS "BRR":LOCATE (12,10):PRINT "Faillite du JOUEUR 2":RUN
180 IF CJ(J)>9.9E+07 THEN CJ(J)=9.9E+07
181 CALL COLOR("0Wb"):LOCATE (17,3):PRINT "JOUEUR 2":LOCATE (19,2)
182 PRINT INT(CJ(2)):CALL COLOR("0WR"):LOCATE (13,3):PRINT "JOUEUR 1"
183 LOCATE (15,2):PRINT INT(CJ(1)):FOR I=1 TO J:CALL SPEECH("L,"&FLI$):PAUSE .3
184 NEXT I:CALL COLOR("0BY"):LOCATE (8,30):PRINT "JOUEUR";J:LOCATE (9,30)
185 PRINT "CARTE<*>":LOCATE (10,30):PRINT "DES  <#>"
186 CALL COLOR("0YB"):LOCATE (20,23):PRINT " * "
187 IF (1-J*PGN)THEN CALL KEY1(ART,TST) ELSE CALL KEY2(ART,TST)
188 IF TST=0 THEN GOTO 187
189 IF ART=42 THEN GOSUB 290
190 IF ART=35 THEN GOTO 194
191 IF ART<>87 THEN 187
192 HH$=KEY$:H1=VAL(HH$):HH$=KEY$:H2=VAL(HH$):CALL COLOR("1WR"):GOTO 201
193 GOTO 187
194 PAUSE .4
195 CALL COLOR("1WR")
196 H1=INTRND(6):H2=INTRND(6)
197 CALL SPEECH("L,"&"0D7C0102BAB602FC")
198 LOCATE (8,6):PRINT H1$(H1):LOCATE (4,10):PRINT H2$(H2)
199 IF (1-J*PGN)THEN CALL KEY1(ARD,TSF) ELSE CALL KEY2(ARD,TSF)
200 IF ARD<>35 THEN GOTO 196
201 INDIC=0
202 LOCATE (T(W(J),15),T(W(J),16))
203 CALL COLOR("1"&CHR$(T(W(J),2))&"G")
204 PRINT CHR$(T(W(J),17))
205 W(J)=W(J)+(H1+H2):IF W(J)>40 THEN W(J)=W(J)-40:CJ(J)=CJ(J)+20000
206 A=W(J)
207 IF H1=H2 THEN DOUB=2
208 GOSUB 372
209 GOSUB 224
210 IF A=30 THEN GOSUB 440:GOTO 163
211 IF T(A,1)=3 THEN GOSUB 412:IF INDIC=1 THEN 201 ELSE GOTO 163
212 IF T(A,1)<>0 AND A=28 THEN GOSUB 394:GOTO 163
213 IF T(A,1)<>0 AND A=12 THEN GOSUB 394:GOTO 163
214 IF T(A,1)<>0 AND T(A,17)=13 THEN GOSUB 375:GOTO 163
215 IF T(A,1)=J AND T(A,5)<5 THEN GOSUB 278:GOTO 163
216 IF T(A,1)=J THEN GOSUB 306:GOTO 163
217 IF T(A,7)<>0 AND T(A,1)=0 THEN GOSUB 254:GOTO 163
218 IF T(A,7)<>0 AND T(A,1)<>0 AND T(A,1)<>J THEN GOSUB 337:GOTO 163
219 IF T(A,1)=8 THEN GOSUB 346:GOTO 163
220 IF T(A,1)=4 THEN GOSUB 350:GOTO 163
221 IF T(A,1)=5 THEN GOSUB 354:GOTO 163
222 IF T(A,1)=6 THEN GOSUB 361:GOTO 163
223 GOTO 163
224 CALL COLOR("0CC")
225 FOR I=1 TO 11:LOCATE (I,16):PRINT RPT$(" ",12):NEXT I
226 CALL COLOR("0"&CUC$(J)&CUF$(J))
227 LOCATE (1,16):PRINT RPT$(CHR$(12),12)
228 LOCATE (2,16):PRINT RPT$(CHR$(12),12)
229 CALL COLOR("0BC")
230 CALL COLOR("0BC"):LOCATE (3,16):PRINT T$(A)
231 IF A=12 OR A=28 THEN GOSUB 246
232 IF T(A,17)=13 THEN GOSUB 239:RETURN
233 IF T(A,13)<>0 THEN 234 ELSE RETURN
234 LOCATE (4,18):PRINT "F";T(A,7):LOCATE (5,16):PRINT "  loyers"
235 LOCATE (6,21):PRINT T(A,8):LOCATE (7,17):PRINT "1 M";T(A,9)
236 LOCATE (8,17):PRINT "2 M";T(A,10):LOCATE (9,17):PRINT "3 M";T(A,11)
237 LOCATE (10,17):PRINT "4 M";T(A,12):LOCATE (11,17):PRINT "Hot";T(A,13)
238 RETURN
239 LOCATE (4,18):PRINT "F";T(A,7)
240 LOCATE (6,20):PRINT "GARES"
241 LOCATE (7,17):PRINT "1 G";T(A,8)
242 LOCATE (8,17):PRINT "2 G";T(A,8)*2
243 LOCATE (9,17):PRINT "3 G";T(A,8)*4
244 LOCATE (10,17):PRINT "4 G";T(A,8)*8
245 RETURN
246 LOCATE (4,18):PRINT "F";T(A,7)
247 LOCATE (6,16):PRINT "Montant du"
248 LOCATE (7,16):PRINT "  LOYER"
249 LOCATE (8,16):PRINT "400 F * DES":LOCATE (9,16):PRINT "Si un joueur"
250 LOCATE (10,16):PRINT "possede  + "
251 CALL COLOR("1YB"):LOCATE (10,24):PRINT CHR$(20)
252 CALL COLOR("1bW"):LOCATE (10,26):PRINT CHR$(14)
253 CALL COLOR("0BC"):LOCATE (11,16):PRINT "800F * DES":RETURN
254 CALL COLOR("0BY"):LOCATE (1,29):PRINT "   vous"
255 LOCATE (2,29):PRINT " achetez ?"
256 LOCATE (4,29):PRINT "*=OUI #=NON"
257 IF (1-J*PGN)THEN CALL KEY1(AC,TS) ELSE CALL KEY2(AC,TS)
258 IF TS=0 THEN 257
259 IF AC<>42 AND AC<>35 THEN 257
260 IF AC=42 THEN T(A,1)=J:CJ(J)=CJ(J)-T(A,7)
261 IF AC=42 THEN LOCATE ((4*J)+10,T(A,14))
262 IF AC=42 THEN CALL COLOR("0"&CHR$(T(A,2))&CHR$(T(A,3)))
263 IF AC=42 THEN PRINT CHR$(T(A,4))
264 RETURN
265 CALL COLOR("0BW"):LOCATE (21,2):PRINT "TERRAINS->"
266 CALL COLOR("0Bb"):LOCATE (21,12):PRINT CHR$(12)&CHR$(12)
267 CALL COLOR("0Wb"):LOCATE (21,14):PRINT CHR$(12)&CHR$(12)&CHR$(12)
268 CALL COLOR("0MM"):LOCATE (21,17):PRINT CHR$(12)&CHR$(12)&CHR$(12)
269 CALL COLOR("0RY"):LOCATE (21,20):PRINT CHR$(12)&CHR$(12)&CHR$(12)
270 CALL COLOR("0RR"):LOCATE (21,23):PRINT CHR$(12)&CHR$(12)&CHR$(12)
271 CALL COLOR("0YY"):LOCATE (21,26):PRINT CHR$(12)&CHR$(12)&CHR$(12)
272 CALL COLOR("0GB"):LOCATE (21,29):PRINT CHR$(12)&CHR$(12)&CHR$(12)
273 CALL COLOR("0bb"):LOCATE (21,32):PRINT CHR$(12)&CHR$(12)
274 CALL COLOR("0BW"):LOCATE (21,34):PRINT "MLNS"
275 CALL COLOR("1RY"):LOCATE (21,38):PRINT CHR$(20)
276 CALL COLOR("1WB"):LOCATE (21,39):PRINT CHR$(14)
277 RETURN
278 CALL COLOR("0BY"):LOCATE (1,29):PRINT "   vous"
279 LOCATE (2,29):PRINT "construisez"
280 LOCATE (4,29):PRINT "*=OUI #=NON"
281 IF (1-J*PGN)THEN CALL KEY1(AC,TS) ELSE CALL KEY2(AC,TS)
282 IF TS=0 THEN GOTO 281
283 IF AC<>42 AND AC<>35 THEN GOTO 281
284 IF AC=42 THEN T(A,5)=T(A,5)+1:CJ(J)=CJ(J)-T(A,6)
285 IF AC=35 THEN GOSUB 306:RETURN
286 IF AC=42 THEN LOCATE ((4*J)+11,T(A,14))
287 IF AC=42 THEN CALL COLOR("1YM")
288 PRINT M1$(T(A,5))
289 RETURN
290 DR=24
291 IF (1-J*PGN)THEN CALL KEY1(DEP,TEP) ELSE CALL KEY2(DEP,TEP)
292 IF TEP=0 THEN GOTO 291
293 IF DEP=35 THEN ART=35:GOSUB 304:RETURN
294 IF DEP=32 THEN GOSUB 299
295 IF DEP=129 THEN DR=DR+1:IF DR>39 THEN GOSUB 304:DR=12:GOSUB 265
296 IF DEP=131 THEN DR=DR-1:IF DR<12 THEN GOSUB 304:DR=39:GOSUB 265
297 CALL COLOR("0RB"):LOCATE (20,DR-1):PRINT " * "
298 GOTO 291
299 FOR I=1 TO 40
300 IF DR=T(I,14)THEN A=I
301 NEXT I
302 GOSUB 224
303 RETURN
304 LOCATE (20,1):PRINT RPT$(" ",40)
305 RETURN
306 PAUSE .3:CALL COLOR("0BY"):LOCATE (1,29):PRINT "Voulez-vous"
307 LOCATE (2,29):PRINT "   vendre ?"
308 LOCATE (4,29):PRINT "*=OUI #=NON"
309 IF (1-J*PGN)THEN CALL KEY1(AC,TS) ELSE CALL KEY2(AC,TS)
310 IF TS=0 THEN GOTO 309
311 IF AC<>42 AND AC<>35 THEN GOTO 309
312 IF AC=35 THEN RETURN
313 IF AC<>42 OR T(A,5)<>0 THEN 316
314 CJ(J)=CJ(J)+(T(A,7)/2):T(A,1)=0:LOCATE ((4*J)+10,T(A,14))
315 CALL COLOR("0MM"):PRINT " ":RETURN
316 IF AC<>42 OR T(A,5)<>1 THEN 319
317 CJ(J)=CJ(J)+(T(A,6)/2):LOCATE ((4*J)+11,T(A,14)):CALL COLOR("0MM"):PRINT " "
318 T(A,5)=0:RETURN
319 CALL COLOR("0BY"):LOCATE (1,29):PRINT "Nombre de  "
320 LOCATE (2,29):PRINT "maisons a  "
321 LOCATE (3,29):PRINT "VENDRE ?  "
322 LOCATE (4,29):PRINT "           "
323 LOCATE (4,33):PRINT CHR$(12)
324 PAUSE .3
325 IF (1-J*PGN)THEN CALL KEY1(UC,US) ELSE CALL KEY2(UC,US)
326 IF US=0 THEN GOTO 325
327 IF UC<48 OR UC>53 THEN 325
328 LOCATE (4,33):PRINT CHR$(UC)
329 UC$=CHR$(UC):NC=VAL(UC$)
330 IF NC=0 THEN RETURN
331 IF NC>T(A,5)THEN 323
332 CJ(J)=CJ(J)+(NC*(T(A,6)/2))
333 LOCATE ((4*J)+11,T(A,14)):CALL COLOR("1WM"):PRINT M1$(T(A,5))
334 T(A,5)=T(A,5)-NC
335 IF T(A,5)=0 THEN LOCATE ((4*J)+11,T(A,14)):CALL COLOR("0MM"):PRINT " "
336 RETURN
337 CJ(J)=CJ(J)-T(A,8+T(A,5))
338 CJ(-J+3)=CJ(-J+3)+T(A,8+T(A,5))
339 CALL COLOR("0RY")
340 LOCATE (2,29):PRINT "Vous payez"
341 LOCATE (3,29):PRINT "le montant"
342 LOCATE (4,29):PRINT "du loyer :"
343 LOCATE (5,29):PRINT "F";T(A,8+T(A,5))
344 FOR I=1 TO 8:CALL SPEECH("L,"&"0D7C3456BAB602FC"):PAUSE .3:NEXT I
345 RETURN
346 CJ(J)=CJ(J)+40000:LOCATE (6,18):CALL COLOR("1RCF"):PRINT "F 40000"
347 LOCATE (8,17):CALL COLOR("1bCL"):PRINT "BBRRAAVVOO"
348 FOR I=1 TO 15:PAUSE .1:CALL SPEECH("L,"&"0D7C1234BAB602FC"):NEXT I
349 RETURN
350 CJ(J)=CJ(J)-15000:LOCATE (6,18):CALL COLOR("1bCF"):PRINT "F -15000"
351 LOCATE (8,17):CALL COLOR("1BC"):PRINT "IMPOSITION"
352 FOR I=1 TO 10:PAUSE .1:CALL SPEECH("L,"&"0D7C4321BAB602FC"):NEXT I
353 RETURN
354 CJ(J)=CJ(J)+15000:LOCATE (6,18):CALL COLOR("1RCF"):PRINT "F +15000"
355 LOCATE (8,17):CALL COLOR("1bC"):PRINT "  OFFERT  "
356 LOCATE (9,17):CALL COLOR("1bC"):PRINT "   par    "
357 LOCATE (10,17):CALL COLOR("1MCHF"):PRINT "   TILT   "
358 LOCATE (11,17):PRINT "   TILT   "
359 FOR I=1 TO 15:PAUSE .6:CALL SPEECH("L,"&"0D7C90ABBAB602FC"):NEXT I
360 RETURN
361 CJ(J)=CJ(J)+(CJ(J)/10)
362 LOCATE (4,17):CALL COLOR("0BC"):PRINT " DON DE L'"
363 LOCATE (5,16):CALL COLOR("0WBLF"):PRINT "AAUUTTEEUURR"
364 CALL COLOR("0BC"):LOCATE (6,17):PRINT "   du     "
365 LOCATE (7,17):PRINT "PROGRAMME "
366 CALL COLOR("0RCHL"):LOCATE (8,17):PRINT "  1100%%  "
367 LOCATE (9,17):PRINT "  1100%%  "
368 CALL COLOR("0BC"):LOCATE (10,17):PRINT " de votre "
369 CALL COLOR("0BC"):LOCATE (11,17):PRINT " CAPITAL. "
370 FOR I=1 TO 15:PAUSE .6:CALL SPEECH("L,"&"0D7CC73DBAB602FC"):NEXT I
371 RETURN
372 LOCATE (T(A,15),T(A,16)):CALL COLOR("1"&CUC$(J)&CUF$(J))
373 PRINT BON$
374 RETURN
375 IF T(A,1)=J THEN 376 ELSE GOTO 383
376 CALL COLOR("0MY")
377 LOCATE (2,29):PRINT "  Vous la"
378 LOCATE (3,29):PRINT " possedez "
379 LOCATE (4,29):PRINT "A VIE....!!"
380 LOCATE (5,29):PRINT "***REPOS***"
381 FOR I=1 TO 6:PAUSE .5:CALL SPEECH("L,"&"0D7CBBE4BAB602FC"):NEXT I
382 RETURN
383 GAR=0:CALL COLOR("0MY"):FOR I=5 TO 35 STEP 10
384 IF T(I,1)=(-J+3)THEN GAR=GAR+1
385 NEXT I:EX=2^(GAR-1)
386 CJ(J)=CJ(J)-(2500*EX)
387 CJ(-J+3)=CJ(-J+3)+(2500*EX)
388 LOCATE (2,29):PRINT GAR;" GARE AU"
389 LOCATE (3,29):PRINT "JOUEUR";(-J+3)
390 LOCATE (4,29):PRINT "Vous payez:"
391 LOCATE (5,29):PRINT "F";2500*EX
392 FOR I=1 TO 6:PAUSE .6:CALL SPEECH("L,"&"0D7C267EBAB602FC"):NEXT I
393 RETURN
394 IF T(A,1)<>J THEN 402
395 CALL COLOR("0bY")
396 LOCATE (2,29):PRINT "  Vous la"
397 LOCATE (3,29):PRINT " possedez"
398 LOCATE (4,29):PRINT "A VIE...!!"
399 LOCATE (5,29):PRINT "***REPOS***"
400 FOR I=1 TO 6:PAUSE .6:CALL SPEECH("L,"&"0D7C5E5EBAB602FC"):NEXT I
401 RETURN
402 IF T(12,1)=(-J+3)AND T(28,1)=(-J+3)THEN COEF=2
403 IF T(A,1)<>T(12,1)AND T(28,1)=(-J+3)THEN COEF=1
404 IF T(A,1)<>T(28,1)AND T(12,1)=(-J+3)THEN COEF=1
405 CJ(J)=CJ(J)-(((H1+H2)*COEF)*400)
406 CALL COLOR("0bY")
407 LOCATE (2,29):PRINT "Vous payez":LOCATE (3,29):PRINT "le montant"
408 LOCATE (4,29):PRINT "des....DES"
409 LOCATE (5,29):PRINT "F";(((H1+H2)*COEF)*400)
410 FOR I=1 TO 6:PAUSE .6:CALL SPEECH("L,"&"0D7CD457BAB602FC"):NEXT I
411 RETURN
412 HCC=INTRND(3)
413 IF HCC=1 THEN GOTO 414 ELSE GOTO 422
414 H=INTRND(7):CALL COLOR("0bC")
415 LOCATE (5,16):PRINT PLU1$(H)
416 LOCATE (6,16):PRINT PLU2$(H)
417 LOCATE (7,17):PRINT "*RECEVEZ*"
418 LOCATE (9,18):PRINT "F";PLU(H)
419 CJ(J)=CJ(J)+PLU(H)
420 FOR I=1 TO 10:PAUSE .4:CALL SPEECH("L,"&"0D7CF473BAB602FC"):NEXT I
421 RETURN
422 IF HCC=2 THEN GOTO 423 ELSE GOTO 431
423 H=INTRND(7):CALL COLOR("0bC")
424 LOCATE (5,16):PRINT MIN1$(H)
425 LOCATE (6,16):PRINT MIN2$(H)
426 LOCATE (7,18):PRINT "*DONNEZ*"
427 LOCATE (9,18):PRINT "F";MIN(H)
428 CJ(J)=CJ(J)-MIN(H)
429 FOR I=1 TO 8:PAUSE .4:CALL SPEECH("L,"&"0D7C6666BAB602FC"):NEXT I
430 RETURN
431 IF A<4 OR A>36 THEN GOTO 412
432 H=INTRN(7):CALL COLOR("0RCF")
433 LOCATE (5,16):PRINT DEP1$(H)
434 LOCATE (6,16):PRINT DEP2$(H)
435 FOR I=1 TO 10:PAUSE .4:CALL SPEECH("L,"&"0D7C6666BAB602FC"):NEXT I
436 IF DER(H)=25 THEN H1=-3:H2=0:INDIC=1:RETURN
437 IF DER(H)=26 THEN H1=+3:H2=0:INDIC=1:RETURN
438 H2=DER(H):H1=-W(J)
439 INDIC=1:RETURN
440 LOCATE (1,3):CALL COLOR("1"&CUC$(J)&CUF$(J)):PRINT BON$
441 LOCATE (11,13):CALL COLOR("0BG"):PRINT "P"
442 CALL COLOR("0BC")
443 LOCATE (6,16):PRINT "VOUS PASSEZ"
444 LOCATE (8,16):PRINT " 3  TOURS"
445 FOR I=1 TO 15:PAUSE .05:CALL SPEECH("L,"&"0D7C7DE3BAB602FC"):NEXT I
446 PR(J)=3:W(J)=10:DOUB=0:RETURN
447 DATA " CHAMPION A"," ECHECOLOR",5000," Erreur de"," la banque",20000
448 DATA POLICHINELLE,"SECOND PRIX",12000,"TOUR D'HANOI","PREMIER PRIX",15000
449 DATA " VOUS LISEZ","   TILT",25000
450 DATA "LE GOLPHABET","No 1 D'OPEN",15000,"VOUS GAGNEZ"," MNEMOLOGIK",9000
451 DATA "VOUS PERDEZ","AVEC MIDWAY",15000
452 DATA "EXELMEMOIR","VOUS COUTE",5000,"VOUS CHANGEZ","D'ORDINATEUR",40000
453 DATA " ABONNEMENT","  A  TILT",9000,"ACHAT JEU DE","DES *10 000*",10000
454 DATA "VOUS ACHETEZ",POLICHINELLE,5000,"VOUS OFFREZ","TOURS HANOI",7500
455 DATA "EN PRISON-->","Desole...",30,"RENDEZ-VOUS","r.DE LA PAIX",39
456 DATA "On y va vite"," a PIGALLE",19,"RECULEZ DE","TROIS CASES",25
457 DATA "RENDEZ-VOUS","GARE DE LYION",15,"AVANCEZ DE","TROIS CASE",26
458 DATA "ALLEZ SUR LA"," CASE DEPART",40
