
100 !-----------------------------------
110 ! PUBLICITE LUMINEUSE DEFILANTE
120 !-----------------------------------
130 TEMP=50
140 CLS "BW"
150 DIM TC$(8)
160 FOR I=1 TO 8
170 READ TC$(I)
180 NEXT I
190 CD=5:CA=6
200 FOR I=1 TO 8
210 CO$="0W"&TC$(I)
220 CALL COLOR(CO$)
230 FOR Y=CD TO CA
240 FOR X=7 TO 8
250 LOCATE (X,Y)
260 PRINT " "
270 NEXT X:NEXT Y
280 CD=CD+4:CA=CA+4
290 NEXT I
300 CALL COLOR("0BHL")
310 LOCATE (10,5)
320 FOR I=1 TO 8
330 PRINT TC$(I);TC$(I);"  ";:NEXT I
340 LOCATE (11,5)
350 FOR I=1 TO 8
360 PRINT TC$(I);TC$(I);"  ";:NEXT I
370 CALL COLOR("0BW")
380 LOCATE (13,1)
390 INPUT "Couleur du texte ";CT$
400 INPUT "Couleur du font  ";CF$
410 LINPUT "Texte? ";TXT$
420 FOR I=1 TO LEN(TXT$)
430 PUB$=PUB$&SEG$(TXT$,I,1)&SEG$(TXT$,I,1)
440 NEXT I
450 POINT$="........................................."
460 PUB$=PUB$&SEG$(POINT$,1,34)
470 LG=LEN(PUB$)
480 COL$=CT$&CF$&CF$
490 CLS (COL$)
500 COLH$="0"&CT$&"HL"
510 CALL COLOR(COLH$)
520 FOR I=2 TO LG STEP 2
525 CALL KEY1(A,B):IF A=27 THEN GOTO 620
530 LOCATE (21,1)
540 IF I<=36 THEN 570
550 PRINT SEG$(PUB$,I-37,38);
560 PRINT SEG$(PUB$,I-37,38);:GOTO 590
570 PRINT SEG$(POINT$,1,38-I);SEG$(PUB$,1,I);
580 PRINT SEG$(POINT$,1,38-I);SEG$(PUB$,1,I);
590 FOR Y=1 TO TEMP:NEXT Y
600 NEXT I
610 GOTO 520
620 END
630 DATA R,G,M,C,Y,b,W,B
