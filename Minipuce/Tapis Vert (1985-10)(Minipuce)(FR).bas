10 ! -------------------------------
20 !          TAPIS VERT
30 ! -------------------------------
40 CALL CHAR(2,"FFFFFFFFFFFFFFFFFFFF")
50 TCOU$(1)="0BW"
60 TCOU$(2)="0bY"
70 TCOU$(3)="0BW"
80 TCOU$(4)="0bY"
90 TTAP(1)=1000
100 TTAP(2)=1000
110 TTAP(3)=1000
120 TTAP(4)=1000
130 CLS "BMb"
140 CO$="0BG":CR=32
150 CALL BOX(1,22,1,20,CO$,CR)
160 FOR I=1 TO 9
170 READ C,L
180 LOCATE (L,C)
190 PRINT I
200 TN(I,1)=C:TN(I,2)=L
210 NEXT I
220 GOTO 240
230 DATA 15,5,5,5,15,9,5,9,10,9,5,13,15,13,5,17,15,17
240 CO$="1BG":CR=2
250 CALL BOX(1,22,1,1,CO$,CR)
260 CALL BOX(1,22,3,3,CO$,CR)
270 CALL BOX(4,19,7,7,CO$,CR)
280 CALL BOX(4,19,11,11,CO$,CR)
290 CALL BOX(4,19,15,15,CO$,CR)
300 CALL BOX(1,22,19,19,CO$,CR)
310 CALL BOX(1,22,21,21,CO$,CR)
320 CALL BOX(1,1,1,21,CO$,CR)
330 CALL BOX(4,4,3,19,CO$,CR)
340 CALL BOX(9,9,3,19,CO$,CR)
350 CALL BOX(14,14,3,19,CO$,CR)
360 CALL BOX(19,19,3,19,CO$,CR)
370 CALL BOX(22,22,1,21,CO$,CR)
380 CALL COLOR("0BM")
390 LOCATE (3,25):PRINT "CASINO"
400 LOCATE (5,25):PRINT "LA BOULE"
410 LOCATE (9,25):PRINT "Nombre de"
420 LOCATE (13,25):PRINT "JEU POSSIBLE"
430 LOCATE (14,25):PRINT "************"
440 LOCATE (16,25):PRINT "Chiffre 1 a 9"
450 LOCATE (17,25):PRINT "A pour  1 a 4"
460 LOCATE (18,25):PRINT "B pour  6 a 9"
470 LOCATE (19,25):PRINT "I pour  impairs"
480 LOCATE (20,25):PRINT "P pour  pairs"
490 LOCATE (10,25):PRINT "joueurs de"
500 LOCATE (11,25):PRINT " 1 a 4 : "
510 A$=KEY$
520 RANDOMIZE
530 IF A$="" THEN RANDOMIZE:GOTO 510
540 IF ASC(A$)<49 OR ASC(A$)>52 THEN GOTO 510
550 A=VAL(A$)
560 LOCATE (11,34):PRINT A$
570 GOSUB 580:GOTO 920
580 CO$="0MM":CR=32
590 CALL BOX(23,39,1,20,CO$,CR)
600 LI1=1:LI2=5:CR=32
610 FOR I=1 TO A
620 CO$=TCOU$(I)
630 CALL BOX(23,39,LI1,LI2,CO$,CR)
640 LOCATE (LI1,23):PRINT "TAPIS :";TTAP(I)
650 IF TTAP(I)=0 THEN MIS=0:GOTO 750
660 LOCATE (LI1+1,23):PRINT "MISE  :        ";
670 W=0:TMI$=""
680 MI$=KEY$
690 RANDOMIZE
700 IF MI$="" THEN RANDOMIZE:GOTO 680
710 IF ASC(MI$)=13 THEN GOTO 740
720 IF ASC(MI$)<48 OR ASC(MI$)>57 THEN GOTO 680
730 W=W+1:LOCATE (LI1+1,30+W):PRINT MI$:TMI$=TMI$&MI$:GOTO 680
740 MIS=VAL(TMI$)
750 IF MIS=0 THEN TMIS(I)=0:TJEU(I)=0:GOTO 890
760 IF MIS>TTAP(I)THEN GOTO 640
770 TMIS(I)=MIS
780 LOCATE (LI1+2,23):PRINT "SUR   : ";
790 JEU$=KEY$
800 RANDOMIZE
810 IF JEU$="" THEN GOTO 790
820 IF JEU$>"0"AND JEU$<"9" THEN GOTO 870
830 IF JEU$="9" THEN GOTO 870
840 IF JEU$="A"OR JEU$="B" THEN GOTO 870
850 IF JEU$="P"OR JEU$="I" THEN GOTO 870
860 GOTO 780
870 PRINT JEU$
880 TJEU$(I)=JEU$
890 LI1=LI1+5:LI2=LI2+5
900 NEXT I
910 RETURN
920 JEU=INTRND(9)
930 IF JEU=5 THEN PAIR=2:AB=2:GOTO 970
940 TSTPAIR=JEU-(INT(JEU/2)*2)
950 IF TSTPAIR=0 THEN PAIR=0 ELSE PAIR=1
960 IF JEU<5 THEN AB=1 ELSE AB=0
970 JEU$=STR$(JEU):JEU$=SEG$(JEU$,1,1)
980 C=TN(JEU,1):L=TN(JEU,2)
990 CALL COLOR("0WR")
1000 LOCATE (L,C)
1010 PRINT JEU;" "
1020 CALL COLOR("0bG")
1030 MT=0
1040 FOR I=1 TO A
1050 IF TMIS(I)=0 THEN GOTO 1160
1060 IF TJEU$(I)=JEU$ THEN GAIN=TMIS(I)*7:GOTO 1140
1070 IF TJEU$(I)="P"AND PAIR=0 THEN GOTO 1130
1080 IF TJEU$(I)="I"AND PAIR=1 THEN GOTO 1130
1090 IF TJEU$(I)="A"AND AB=1 THEN GOTO 1130
1100 IF TJEU$(I)="B"AND AB=0 THEN GOTO 1130
1110 TTAP(I)=TTAP(I)-TMIS(I)
1120 LOCATE (I*5,23):PRINT "PERDU":GOTO 1160
1130 GAIN=TMIS(I)*2
1140 TTAP(I)=TTAP(I)+GAIN
1150 LOCATE (I*5,23):PRINT "GAIN  :";GAIN
1160 IF TTAP(I)>0 THEN MT=MT+1
1170 NEXT I
1180 IF MT=0 THEN GOTO 1290
1190 LOCATE (22,25):PRINT "Touche entree"
1200 A$=KEY$
1210 RANDOMIZE
1220 IF A$="" THEN RANDOMIZE:GOTO 1200
1230 LOCATE (L,C)
1240 CALL COLOR("0BG")
1250 PRINT JEU;" "
1260 CALL COLOR("0MM")
1270 LOCATE (22,25):PRINT "            "
1280 GOTO 570
1290 GOSUB 580
1300 CALL COLOR("0MW")
1310 LOCATE (22,30):PRINT "FIN"
1320 PAUSE
1330 CLS "BCC":END
1340 SUB BOX(CD,CA,LD,LA,CO$,CR)
1350 CALL COLOR(CO$)
1360 FOR Y=LD TO LA
1370 FOR X=CD TO CA
1380 LOCATE (Y,X):PRINT CHR$(CR);
1390 NEXT X
1400 NEXT Y
1410 SUBEND
