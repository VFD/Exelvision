10 ! ----------------------------------
20 !              PETANQUE
30 ! ----------------------------------
40 DEG
50 DIM PO(4,2)
60 CLS "bMM"
70 CALL COLOR("0bBHL")
80 LOCATE (10,7):PRINT "PPEETTAANNQQUUEE"
90 LOCATE (11,7):PRINT "PPEETTAANNQQUUEE"
100 CALL COLOR("0RY")
110 LOCATE (20,5):PRINT "Appuyez sur une touche pour jouer"
120 A$=KEY$:RANDOMIZE:IF A$="" THEN RANDOMIZE:GOTO 120
130 !
140 NBJ=2
150 CLS
160 CALL HRON("W",2,18)
170 LOCATE (1,17):PRINT "PETANQUE"
180 SCO(1)=0:SCO(2)=0
190 CALL BOX(1,27,20,22,"0bY")
200 CALL BOX(28,39,20,22,"0WR")
210 FOR X=1 TO 4:PO(X,1)=999:PO(X,2)=999:NEXT X
220 CALL COLOR("0WR")
230 LOCATE (20,30)
240 PRINT "J1 ";SCO(1)
250 LOCATE (22,30):PRINT "J2 ";SCO(2)
260 CALL COLOR("0bY")
270 LOCATE (20,1):PRINT "Joueur "
280 LOCATE (21,1):PRINT "Force :"
290 LOCATE (22,1):PRINT "Angle :"
300 NBB(1)=2
310 NBB(2)=2
320 GOSUB 1640
330 W=-2
340 FOR J=1 TO 2
350 W=W+2
360 FOR I=1 TO NBJ
370 CALL COLOR("0bY")
380 LOCATE (20,8):PRINT I
390 LOCATE (21,9):PRINT "   "
400 LOCATE (22,9):PRINT "   "
410 X=0:FRC$=""
420 A$=KEY$
430 IF A$="" THEN GOTO 420
440 IF ASC(A$)=13 AND FRC$="" THEN GOTO 420
450 IF ASC(A$)=13 THEN GOTO 490
460 IF ASC(A$)<48 OR ASC(A$)>57 THEN GOTO 420
470 LOCATE (21,X+9):PRINT A$
480 FRC$=FRC$&A$:X=X+1:GOTO 420
490 FRCE=VAL(FRC$):IF FRCE=0 THEN GOTO 420
500 X=0:ANGL$=""
510 A$=KEY$
520 IF A$="" THEN GOTO 510
530 IF ASC(A$)=13 AND ANGL$="" THEN GOTO 510
540 IF ASC(A$)=13 THEN GOTO 580
550 IF ASC(A$)<48 OR ASC(A$)>57 THEN GOTO 510
560 LOCATE (22,X+9):PRINT A$
570 ANGL$=ANGL$&A$:X=X+1:GOTO 510
580 ANGLE=VAL(ANGL$)
590 IF ANGLE=0 THEN GOTO 500
600 CD=3:LD=3
610 LA=FRCE*10*COS(ANGLE)+LD
620 CA=FRCE*8*SIN(ANGLE)+CD
630 IF CA>318 OR LA>178 THEN GOSUB 1860:GOTO 1070
640 CA=ABS(INT(CA)):LA=ABS(INT(LA))
650 LP=3:CP=3:Q=0
660 IF I=1 THEN C$="R" ELSE C$="b"
670 !
680 ! *** TRACE DU PASSAGE DE LA BOULE
690 !
700 ! ***
710 IF CA<LA THEN INCC=CA/LA ELSE GOTO 820
720 FOR LP=3 TO LA
730 CALL PLOT("W",CP-Q,LP-1)
740 GOSUB 1950
750 IF TR=1 THEN GOTO 1020
760 CALL PLOT("B",CP,LP)
770 CP=CP+INCC:Q=INCC
780 NEXT LP
790 CALL PLOT("W",CP-Q,LP-1)
800 GOTO 1020
810 ! ***
820 IF CA>LA THEN INCL=LA/CA ELSE GOTO 920
830 FOR CP=3 TO CA
840 CALL PLOT("W",CP-1,LP-Q)
850 GOSUB 1950
860 IF TR=1 THEN GOTO 1020
870 CALL PLOT("B",CP,LP)
880 LP=LP+INCL:Q=INCL
890 NEXT CP
900 CALL PLOT("W",CP-1,LP-Q)
910 GOTO 1020
920 ! ***
930 FOR CP=3 TO CA
940 CALL PLOT("W",CP-1,LP-1)
950 GOSUB 1950
960 IF TR=1 THEN GOTO 1020
970 CALL PLOT("B",CP,LP)
980 LP=LP+1
990 NEXT CP
1000 CALL PLOT("B",CP-1,LP-1)
1010 ! ***
1020 CALL LINE(C$,CA-2,LA,CA+2,LA)
1030 CALL LINE(C$,CA-2,LA-1,CA+2,LA-1)
1040 CALL LINE(C$,CA-2,LA+1,CA+2,LA+1)
1050 CALL LINE(C$,CA-1,LA-2,CA+1,LA-2)
1060 CALL LINE(C$,CA-1,LA+2,CA+1,LA+2)
1070 NBB(I)=NBB(I)-1
1080 IF J=1 THEN PO(J-1+I,1)=CA:PO(J-1+I,2)=LA ELSE PO(J+I,1)=CA:PO(J+I,2)=LA
1090 !
1100 NEXT I
1110 NEXT J
1120 !
1130 !*** CALCUL DES DISTANCES
1140 !*** BOULE _ COCHONNET
1150 !
1160 FOR N=1 TO 4
1170 O=PO(N,1)-CCH:O=O*O
1180 P=PO(N,2)-LCH:P=P*P
1190 DIS(N)=ABS(SQR(O+P))
1200 NEXT N
1210 !
1220 !*** RECHERCHE PLUS PETITE DISTANCE
1230 !
1240 REF=1
1250 FOR N=1 TO 4
1260 IF DIS(N)<DIS(REF)THEN REF=N
1270 NEXT N
1280 GAN=REF-(INT(REF/2)*2)
1290 GAN=(ABS(GAN-1))+1
1300 SCO(GAN)=SCO(GAN)+1:TG=1
1310 DIS(REF)=9999
1320 !
1330 !*** RECHERCHE SECONDE DISTANCE
1340 !
1350 REF=1
1360 FOR I=1 TO 4
1370 IF DIS(I)<DIS(REF)THEN REF=I
1380 NEXT I
1390 GAN2=REF-(INT(REF/2)*2)
1400 GAN2=(ABS(GAN2-1))+1
1410 IF GAN2=GAN THEN SCO(GAN)=SCO(GAN)+1:TG=2
1420 LOCATE (20,1):PRINT "Le joueur";GAN;"GAGNE";TG;"POINT";
1430 IF TG=2 THEN PRINT "S"
1440 !
1450 !*** AFFICHAGE GAGNANT
1460 !
1470 IF GAN=1 THEN LOCATE (20,34) ELSE LOCATE (22,34)
1480 PRINT SCO(GAN);
1490 IF SCO(GAN)<4 THEN GOTO 1510
1500 LOCATE (20,1):PRINT "LE JOUEUR";GAN;"GAGNE LA PARTIE":PAUSE 3:GOTO 1750
1510 GOSUB 1550:CLS:GOTO 190
1520 !
1530 !  R.A.Z DE L'ECRAN HRON
1540 !
1550 PAUSE 3:CP=CCH:LP=LCH:GOSUB 2050
1560 FOR Z=1 TO 4
1570 IF PO(Z,1)=999 THEN GOTO 1590
1580 CP=PO(Z,1):LP=PO(Z,2):GOSUB 2050
1590 NEXT Z
1600 RETURN
1610 !
1620 ! **** LANCER DU COCHONNET *****
1630 !
1640 CCH=INTRND(30)
1650 IF CCH<7 THEN CCH=CCH+10
1660 CCH=CCH*8
1670 LCH=INTRND(19)
1680 LCH=LCH*8
1690 CALL LINE("B",CCH-1,LCH,CCH+1,LCH)
1700 CALL LINE("B",CCH,LCH-1,CCH,LCH+1)
1710 RETURN
1720 !
1730 ! *** FIN DE PARTIE ****
1740 !
1750 GOSUB 1550
1760 CLS
1770 LOCATE (13,1):PRINT "Voulez vous faire une autre partie ?"
1780 A$=KEY$:RANDOMIZE
1790 IF A$="" THEN RANDOMIZE:GOTO 1790
1800 IF SEG$(A$,1,1)="O" THEN GOTO 140
1810 CLS:LOCATE (10,10):PRINT "A bientot ... j'espere"
1820 PAUSE 2:CALL HROFF:CLS "BCC":END
1830 !
1840 ! *** BOULE HORS LIMITE ***
1850 !
1860 LOCATE (22,1):PRINT "OUT                        "
1870 LOCATE (21,1):PRINT "                           "
1880 PAUSE 2:CA=0:LA=0
1890 LOCATE (21,1):PRINT "Force :"
1900 LOCATE (22,1):PRINT "Angle :"
1910 RETURN
1920 !
1930 ! *** RECHERCHE D'OBSTACLE ****
1940 !
1950 TR=0
1960 CT=CCH:LT=LCH:GOSUB 2110
1970 IF TR=1 THEN RETURN
1980 FOR X=I+W TO 1 STEP -1
1990 IF PO(X,1)=999 THEN GOTO 2030
2000 CT=PO(X,1):LT=PO(X,2)
2010 GOSUB 2110
2020 IF TR=1 THEN RETURN
2030 NEXT X
2040 RETURN
2050 CALL LINE("W",CP-2,LP-1,CP+2,LP-1)
2060 CALL LINE("W",CP-2,LP+1,CP+2,LP+1)
2070 CALL LINE("W",CP-2,LP,CP+2,LP)
2080 CALL LINE("W",CP-1,LP-2,CP+1,LP-2)
2090 CALL LINE("W",CP-1,LP+2,CP+1,LP+2)
2100 RETURN
2110 IF (CP<CT+3 AND CP>CT-3)AND (LP>LT-3 AND LP<LT+3)THEN GOTO 2120 ELSE RETURN
2120 CA=CP-3
2130 LA=LP-3
2140 TR=1
2150 RETURN
2160 SUB BOX(CD,CA,LD,LA,CO$)
2170 CALL COLOR(CO$)
2180 FOR Y=LD TO LA
2190 FOR X=CD TO CA
2200 LOCATE (Y,X):PRINT " "
2210 NEXT X
2220 NEXT Y
2230 SUBEND
