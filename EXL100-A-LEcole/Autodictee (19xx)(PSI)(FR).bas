100 ON BREAK ERROR:ON ERROR 1140
110 !********** AUTODICTEE ***********
120 !******* VERSION EXL 100 *********
130 !**** D.NIELSEN - 1983 ***********
140 CLS "CBB":CALL INIT
150 LOCATE (10,6):CALL COLOR("0YLH")
160 PR INT "AAUUTTOODDIICCTTEEEE":LOCATE (11,6):PRINT "AAUUTTOODDIICCTTEEEE"
170 CALL COLOR("0RB")
180 PRINT "VOUS ALLEZ ENTRER VOTRE TEXTE"
190 CALL COLOR ("0MB"):PRINT "ATTENTION "
200 CALL COLOR ("0RB"):PRINT "DEUX LIGNES MAXIMUM PAR PHRASE !"
210 LOCATE (20,1):PRINT "POUR COMMENCER,TAPEZ LA TOUCHE 'C'"
220 CALL KEY1(A,B):IF A<>67 OR B<>1 THEN 220
230 CLS "BCC"
240 CALL COLOR("0bC"):PRINT "PREMIERE PHRASE"
250 CALL COLOR("0BC"):LINPUT A$
260 PRINT
270 CALL COLOR("0bC"):PRINT "DEUXIEME PHRASE"
280 CALL COLOR("0BC"):LINPUT B$
290 PRINT
300 CALL COLOR("0bC"):PRINT "TROISIEME PHRASE"
310 CALL COLOR("0BC"):LINPUT C$
320 PRINT
330 CALL COLOR("0bC"):PRINT "QUATRIEME PHRASE"
340 CALL COLOR("0BC"):LINPUT D$
350 PRINT
360 CALL COLOR("0bC"):PRINT "CINQUIEME PHRASE"
370 CALL COLOR("0BC"):LINPUT E$
380 CLS:CALL HRON("C",4,1):CALL LINOF
390 LOCATE (2,6):CALL COLOR("0bLH")
400 PRINT "AAUUTTOODDIICCTTEEEE":LOCATE(3,6):PRINT "AAUUTTOODDIICCTTEEEE"
410 CALL COLOR("0BC")
420 V$=A$
430 T=0
440 X=0
450 LINPUT "QUEL EST TON PRENOM ?";Z$
460 GOSUB 630
470 V$=B$:GOSUB 630
480 V$=C$:GOSUB 630
490 V$=D$:GOSUB 630
500 V$=E$:GOSUB 630
510 CALL HROFF:CALL LINOF
520 CALL COLOR("0RLH"):FOR L=10 TO 11
530 LOCATE (L,4):FOR K=1 TO LEN(Z$)
540 PRINT RPT$(SEG$(Z$,K,1),2);
550 NEXT K:NEXT L:CALL COLOR("0BC"):LOCATE (15,1)
560 IF X>1 THEN PRINT "TON SCORE TOTAL EST DE";X;"FAUTES"
570 IF X<=1 THEN PRINT "TON SCORE TOTAL EST DE";X;"FAUTE"
580 IF X<5 THEN PRINT "C'EST TRES BIEN"
590 IF X>=5 AND X<=10 THEN PRINT "C'EST UN BON RESULTAT"
600 IF X>10 THEN PRINT "IL FAUDRA ETRE PLUS ATTENTIF LA PROCHAINE FOIS"
610 PRINT:PRINT "APPELLE UN CAMARADE","POUR FAIRE L'EXERCICE"
620 PAUSE 10:GOTO 380
630 CLS
640 M=0
650 T=0
660 PRINT "TU VAS LIRE CETTE PHRASE":PRINT "LORSQUE TU LA CONNAITRAS BIEN,"
670 PRINT "APPUIE SUR 'C'"
680 LOCATE (12,1):CALL COLOR("0bB")
690 PRINT V$&RPT$(" ",80-LEN(V$));
700 CALL COLOR("0BC")
710 CALL KEY1(A,B):IF A<>67 OR B<>1 THEN 710
720 X=X+T
730 T=0
740 IF M=3 THEN 950
750 CLS:LOCATE (8,1)
760 CALL COLOR("0RB"):PRINT "TAPE LE TEXTE,SANS OUBLIER LA PONTUATION"
770 PRINT "     ET APPUIE SUR LA TOUCHE 'PRET'      "
780 LOCATE (19,1)
790 CALL COLOR("0BC"):LINPUT M$
800 CALL COLOR("0RC"):LOCATE (8,1):PRINT RPT$(" ",80)
810 IF LEN(M$)<>LEN(V$) THEN GOSUB 1020:GOTO 740
820 FOR I=1 TO LEN (V$ )
830 CALL COLOR("0bC"):LOCATE(12+INT(I/40),I+1-40*INT(I/40))
840 IF SEG$(M$,I,1)=SEG$(V$,I,1) THEN PRINT SEG$(M$,I,1):GOTO 860
850 GOSUB 1150
860 NEXT I
870 M=M+1:LOCATE (6,1)
880 IF T=0 THEN PRINT "TU N'AS FAIT AUCUNE FAUTE,";Z$:PRINT " C'EST TRES BIEN"
890 IF T=0 THEN CALL PLAY("DOREREDORE",5)
900 IF T=1 AND M<3 THEN PRINT "TU AS FAIT";T;"FAUTE.":GOTO 920
910 IF T>1 AND M<3 THEN PRINT "TU AS FAIT";T;"FAUTES."
920 IF T>=1 AND M<3 THEN PRINT "TU VAS POUVOIR RECOMMENCER LE TEXTE."
930 PAUSE 5
940 IF T>0 AND M<3 THEN 720
950 IF T>=1 AND M=3 THEN PRINT "VOICI LE TEXTE EXACT :":CALL COLOR("0RB")
960 IF T>=1 AND M=3 THEN PRINT V$;RPT$(" ",80-LEN(V$)):CALL COLOR("0BC")
970 IF T>1 AND M=3 THEN PRINT "TU AS FAIT";T;"FAUTES."
980 IF T=1 AND M=3 THEN PRINT "TU AS FAIT UNE FAUTE."
990 X=X+T
1000 PAUSE 6
1010 RETURN
1020 PRINT "TU AS FAIT UNE ERREUR : TON TEXTE N'A"
1030 PRINT "PAS LE MEME NOMBRE DE LETTRES QUE LE"
1040 PRINT "PREMIER. IL FAUT RECOMMENCER."
1050 K=LEN(V$)-LEN(M$)
1060 IF K>1 THEN PRINT "IL MANQUE";K;"LETTRES DANS TON TEXTE."
1070 IF K= 1 THEN PRINT "IL MANQUE UNE LETTRE DANS TON TEXTE."
1080 IF K<-1 THEN PRINT "TON TEXTE A";-K;"LETTRES DE TROP."
1090 IF K=-1 THEN PRINT "TON TEXTE A UNE LETTRE DE TROP"
1100 PAUSE 5
1110 T=T+1
1120 M=M+1
1130 RETURN
1140 CALL HROFF:CLS "BCC":END
1150 CALL COLOR("0RR"):PRINT " ";:CALL COLOR("0RC"):T=T+1
1160 CALL PLAY("SIDO",25):RETURN
1170 SUB INIT:CALL HROFF:CALL LINOF
1180 CALL POKE(50688,165,8,6,10):CALL POKE(49156,198,0)
1190 SUBEND
1200 SUB LINOF
1210 CALL POKE(50850,162,5,45,162,136,45,10):CALL EXEC(50850):SUBEND
1220 SUB PLAY(NO$,L)
1230 FOR I=1 TO LEN(NO$) STEP 2
1240 K=POS("DO.RE.MI.FA.SO.LA.SI.",SEG$(NO$,I,2),1)
1250 PER=VAL(SEG$("199176158149133118105",K,3))
1260 CALL POKE(258,PER,132):PAUSE L/50:CALL POKE(259,45)
1270 NEXT:SUBEND
