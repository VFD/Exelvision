100 !*********** SOUTI EN *************
110 !*** QUESTIONS A REPONSES MULTIPLES
120 !******* VERSION EXL 100 *********
130 !**** D.NIELSEN - 1985 ***********
140 DIM Q$(8),R$(8),Y(8)
150 CALL INIT:CLS "BCC":CALL COLOR( " O RLH " )
160 LOCATE (10,10):PRINT "SSOOUUTTIIEENN":LOCATE (11,10):PRINT "SSOOUUTTIIEENN"
170 CALL COLOR("0bC"):PRINT:PRINT:PRINT "CE PROGRAMME VA DEMANDER DE CHOISIR"
180 PRINT "QUATRE REPONSES POUR QUATRE QUESTIONS"
190 PRINT "POSEES SEPAREMENT.IL Y A DEUX SERIES DE QUESTIONS."
200 PRINT "vous ALLEZ D'ABORD ENTRER LES DONNEES.  ATTENTION :"
210 PRINT "IL NE DOIT PAS Y AVOIR DEUX REPONSES":PRINT "IDENTIQUES DANS UNE ";
220 PRINT "SERIE.":PRINT:PRINT:PRINT "TAPEZ UNE TOUCHE POUR LA SUITE..."
230 A$=KEY$:CLS
240 CALL COLOR("0bLH"):LOCATE (1,1):PRINT "EENNTTRREEEE DDEESS DDOONNNNEEEESS"
250 LOCATE (2,1):PRINT "EENNTTRREEEE DDEESS DDOONNNNEEEESS":CALL COLOR("0BC")
260 LOCATE (5,1)
270 FOR I=1 TO 4
280 CALL COLOR("0BC"):PRINT "TAPEZ LA PHRASE NO";I
290 CALL COLOR("0bC"):LINPUT Q$(I)
300 CALL COLOR("0BC"):PRINT "TAPEZ SA REPONSE "
310 CALL COLOR("0bC"):LINPUT R$(I)
320 NEXT I
330 PRINT:PRINT
340 PRINT "MAINTENANT,LA DEUXIEME SERIE"," DE QUESTIONS":PRINT:PRINT
350 FOR I=5 TO 8
360 CALL COLOR("0BC"):PRINT "TAPEZ LA PHRA SE NO" ;I
370 CALL COLOR("0bC"):LINPUT Q$(I)
380 CALL COLOR("0BC"):PRINT "TAPEZ SA REPONSE"
390 CALL COLOR("0bC"):LINPUT R$(I)
400 NEXT I
410 CLS "BCC":CALL COLOR("0bC")
420 PRINT "DONNEZ UN TITRE A CETTE SERIE":PRINT:PRINT
430 CALL COLOR("0BC"):LINPUT U$
440 CALL COLOR("0bC")
450 C=LEN(Q$(1))
460 FOR I=1 TO C
470 RANDOMIZE
480 NEXT I
490 L,M,N,P=0
500 CLS
510 PRINT "TU VAS AVOIR LE CHOIX ENTRE QUATRE","REPONSES POUR CHAQUE QUESTION."
520 PRINT "LES QUESTIONS SONT ECRITES EN VERT."
530 PRINT "TU DEVRAS INDIQUER LE NUMERO DE LA BONNE REPONSE."
540 PRINT:PRINT:PRINT
550 LINPUT "QUEL EST TON PRENOM ?";Z$
560 CLS
570 T,V=0
580 JEU=1:GOSUB 930
590 !1ere serie
600 FOR I=JEU TO JEU+3
610 GOSUB 1030
620 CALL COLOR("0GB"):LOCATE (18,1):PRINT Q$(I):CALL COLOR("0bC")
630 PRINT:PRINT:INPUT "TA REPONSE ?";REP:REP=REP+JEU-1
640 IF Y(REP)=I THEN GOSUB 850 ELSE GOSUB 880
650 PAUSE 5
660 CLS:NEXT I
670 IF JEU=5 THEN 720
680 !2eme serie
690 JEU=JEU+4
700 GOSUB 930
710 GOTO 600
720 !fin de jeu
730 LOCATE (12,1)
740 PRINT Z$;",TON SCORE EST DE";T;"POINTS.","(2 POINTS PAR BONNE REPONSE,"
750 PRINT " ET 1 POINT EN MOINS PAR MAUVAISE.) "
760 CALL COLOR("0RB")
770 IF T>=15 THEN PRINT "C'EST TRES BIEN."
780 IF T<15 AND T>8 THEN PRINT "BIEN,MAIS SOIS PLUS ATTETIF."
790 IF T<=8 THEN PRINT "IL FAUDRA REVOIR LE TRAVAIL CORRESPON-","DANT."
800 CALL COLOR ("0bC")
810 PRINT:PRINT:PRINT
820 PRINT "APPELLE UN CAMARADE POUR FAIRE CE JEU"
830 PAUSE 15
840 GOTO 450
850 CALL PLAY("SOLASILASO",2)
860 PRINT "C'EST EXACT,";Z$;",BRAVO."
870 T=T+2:RETURN
880 CALL PLAY("DODOMISOSO",2)
890 PRINT "TU N'AS PAS TROUVE,";Z$
900 PRINT "LA BONNE REPONSE ETAIT: ";:CALL COLOR ("0RB"):PRINT R$(I)
910 T=T-1
920 CALL COLOR("0bC"):PAUSE 5:RETURN
930 !place des repenses
940 FOR I=JEU TO JEU+3
950 Y(I)=INTRND(4)+JEU-1
960 IF I=JEU THEN 1000
970 FOR K=JEU TO I-1
980 IF Y(I)=Y(K) THEN 950
990 NEXT K
1000 NEXT I
1010 RETURN
1020 !affichage des reponses
1030 CLS:LOCATE (1,1):CALL COLOR ("0BC"):PRINT U$
1040 PRINT:PRINT TAB(10);Z$;" =";T;"POINT";
1050 IF T<>0 AND T<>1 THEN PRINT "S" ELSE PRINT
1060 FOR K=1 TO 4
1070 LOCATE (K*2+5,10)
1080 AF=K+JEU-1
1090 CALL COLOR("0bC")
1100 PRINT R$(Y(AF))&"  ?"
1110 CALL COLOR("0"&SEG$("RbMG",K,1)&"B")
1120 LOCATE (K*2+5,7):PRINT K;
1130 NEXT K
1140 CALL COLOR("0bC")
1150 RETURN
1160 END
1170 SUB INIT
1180 CALL POKE(50580,162,5,45,162,136,45,10):CALL EXEC(50580)
1190 CALL POKE(50688,165,8,6,10):CALL POKE(49156,198,0)
1200 SUBEND
1210 SUB PLAY(NO$,L)
1220 FOR I=1 TO LEN(NO$) STEP 2
1230 K=POS("DO.RE.MI.FA.SO.LA.SI.",SEG$(NO$,I,2),1)
1240 P=VAL(SEG$("199176158149133118105",K,3))
1250 CALL POKE (258,P,132):PAUSE L/50:CALL POKE(259,45)
1260 NEXT:SUBEND
