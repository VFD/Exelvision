1 REM ******************************** 
2 REM * VOCABULAIRE LATIN            *
3 REM *      SUR EXELVISION          *    
4 REM *          SARHY-MANGIN   J-CL * 
5 REM *                 LE 10/12/84  * 
6 REM ******************************** 
7 REM VARIABLES 
8 REM *CH=CHOIX MENU*CHOIX=CHOIX EXERCICE*MAT$=MOT A TRADUIRE*DAT$=DATA A LIRE 
9 REM TR$=TRADUCTION*NO$=NOMINATIF*GE$=GENITIF*G$=GENRE*MF$=MOT FRANCAIS * 
10 REM DEB=DEBUT DES DATAS*FI=FIN DES DATAS PAR FAMILLE*FIN=FIN DE TS LES DATAS 
11 REM H OU HA=HASARD*Q$=QUESTION*RJ$=REPONSE JUSTE(A TROUVER)*RE$=REP. ELEVE 
12 DIM MO$(40),MN$(40) 
13 CLS ("RWW"):CALL COLOR("0RWHLF") 
14 LOCATE (12,18):PRINT "LLAATTIINN" 
15 LOCATE (13,18):PRINT "LLAATTIINN" 
16 CALL COLOR("0RW") 
17 LOCATE (22,35):PRINT "<--'":PAUSE 
18 CLS:LOCATE (5,3):PRINT "PRECISIONS :" 
19 LOCATE (6,3):PRINT "------------"  
20 M1$="      CE PROGRAMME PERMET DE REVISER UNEPARTIE DU VOCABULAIRE LATIN" 
21 M2$=" (ICI CL. 4e)APRES AVOIR RENTRE ,SOUS  FORME DE DATASLES  MOTS  APPRIS" 
22 M3$=" A CHAQUE LECON." 
23 LOCATE (9,1):PRINT M1$&M2$&M3$ 
24 M4$="VOUS POURREZ TESTER VOS CONNAISSANCESEN EFFECTUANT " 
25 M5$="DES  TRADUCTIONS  DANS  LESENS FRANCAIS -> LATIN SI VOUS AVEZ RETENU " 
26 M6$="LE CHOIX [4] ET RECIPROQUEMENT [CHOIX=3],LES MOTS A TRADUIRE VOUS " 
27 M7$=" ETANT PRESENTES DE FACON ALEATOIRE." 
28 PRINT:PRINT "   "&M4$&M5$&M6$&M7$ 
29 LOCATE (22,35):PRINT "<--'":PAUSE 
30 CLS:LOCATE (3,3):PRINT "PRECISIONS :" 
31 LOCATE (4,3):PRINT "------------"  
32 PRINT:::: 
33 PRINT " A L'INTERIEUR DES CHOIX [3] ET [4]VOUS POURREZ REVISER :" 
34 PRINT TAB(14);"-......LES MOTS INVARIABLES" 
35 PRINT TAB(14);"-.............LES ADJECTIFS" 
36 PRINT TAB(14);"-................LES VERBES" 
37 PRINT TAB(14);"-..................LES NOMS" 
38 PRINT "SELON VOTRE CHOIX.":PRINT 
39 M13$="    EN RENSEIGNANT   [DEBUT DES DATAS ?](DE 1 A N) VOTRE REVISION " 
40 M14$="COMMENCERA  AU1er OU AU  Nieme MOT ENTRE." 
41 M15$="    EN RENSEIGNANT  [CHOIX  DU HASARD ?](DE 1 A N) LE SAUT D 'UN " 
42 M16$="DATA A  L'AUTREAURA UNE VALEUR ALEATOIRE COMPRISE ENTREC[1 ET N]." 
43 PRINT M13$&M14$ 
44 PRINT M15$&M16$:PRINT 
45 LOCATE (22,36):PRINT "<--'":PAUSE 
46 CLS:LOCATE (5,3):PRINT "PRECISIONS :" 
47 LOCATE (6,3):PRINT "------------"
48 M8$="    SI VOUS NE CONNAISSEZ PAS LA REPONSEA UNE QUESTION FRAPPEZ [T] " 
49 M9$="(COMME TRADUCTION) ET LA REPONSE APPARRAITRA." 
50 LOCATE (9,1):PRINT M8$&M9$ 
51 M10$="    A CHAQUE INSTANT  VOUS  POURREZ REVENIR AU MENU EN FRAPPANT [M]." 
52 M1l$="LE CHOIX [5]VOUS PERMETTRA DE TRAVAILLER VOS ERREURSCELLES-CI AYANT " 
53 M12$="ETE MEMORISEES; LES CHOIX[1]ET[2] VOUS OUVRIRONT LE DICTIONNAIRE." 
54 PRINT::::PRINT M10$&M11$&M12$ 
55 PRINT:PRINT "    LE CHOIX [6] VIDERA LA MEMOIRE  DES ERREURS" 
56 LOCATE (22,36):PRINT "<--'":PAUSE 
57 CLS ("RWW")::CALL COLOR("0RWL"):LOCATE (4,16):PRINT "**MMEENNUU**": 
58 LOCATE (5,16):PRINT "------------":  
59 CALL COLOR("0RW"):LOCATE (8,3):PRINT "1.DICTIONNAIRE LATIN --> FRANCAIS" 
60 LOCATE (10,3):PRINT "2.DICTIONNAIRE FRANCAIS --> LATIN" 
61 LOCATE (12,3):PRINT "3.VERSION (EXERCICES)" 
62 LOCATE (14,3):PRINT "4.THEME (EXERCICES) " 
63 LOCATE (16,3):PRINT "5.LECTURE DES ERREURS" 
64 LOCATE (18,3):PRINT "6.REMISE A ZERO MEMOIRE ERREURS" 
65 LOCATE (22,22):PRINT "VOTRE CHOIX ?"; 
66 ACCEPT VALIDATE("123456")NULL(0)SIZE(1)BEEP;CH:IF CH=0 THEN 65 
67 CLS:ON CH GOTO 69,69,96,96,283,318 
68 GOTO 68 
69 REM ******************************
70 REM * DICTIONNAIRE L->F ET F->L  * 
71 REM ****************************** 
72 CALL COLOR("0RWL") 
73 IF CH<>1 THEN 76 
74 LOCATE (4,8):PRINT "DDIICCTTIIOONNNNAAIIRREE" 
75 LOCATE (6,15):PRINT "LL-->>FF":GOTO 78 
76 LOCATE (4,8):PRINT "DDIICCTTIIOONNNNAAIIRREE" 
77 LOCATE (6,15):PRINT "FF-->>LL" 
78 CALL COLOR("0RW"):LOCATE (11,3):PRINT "* MOT A TRADUIRE : " 
79 LOCATE (15,1):PRINT RPT$("          ",16) 
80 LOCATE (20,1):PRINT RPT$("          ",11)&"        " 
81 LOCATE (13,12):CALL COLOR("0bW"):ACCEPT BEEP;MAT$ 
82 RESTORE 174 
83 NO$="":GE$="":G$="":MF$="" 
84 READ NO$,GE$,G$,MF$ 
85 IF CH=1 THEN DAT$=NO$:TR$=MF$:GOTO 87 
86 DAT$=MF$:TR$=NO$ 
87 IF NO$="FIN"THEN LOCATE (15,5):PRINT "JE NE CONNAIS PAS CE MOT":GOTO 93 
88 IF MAT$=DAT$THEN 89 ELSE 84 
89 CALL COLOR("0RW"):LOCATE (15,3):PRINT "* TRADUCTION     :";  
90 LOCATE (17,12):CALL COLOR("0MW") 
91 PRINT TR$;:IF CH=2 THEN PRINT ",";GE$;",";G$ 
92 CALL COLOR("0RW") 
93 LOCATE (20,3):PRINT "* VOULEZ-VOUS CONTINUER ?" 
94 LOCATE (22,10):PRINT " (MENU = [M] , OUI = <--') ?" 
95 IF KEY$="M"THEN 57 ELSE 78 
96 REM ******************************* 
97 REM * VERSION ET THEMES EXERCICES * 
98 REM ******************************* 
99 CLS 
100 IF FA>39 THEN GOSUB 154 
101 IF CH<>3 THEN 106 
102 CALL COLOR("0RWL") 
103 LOCATE (2,15):PRINT "VVEERRSSIIOONN" 
104 LOCATE (3,15):PRINT "--------------"  
105 CALL COLOR("0RW"):GOTO 110 
106 CALL COLOR("0RWL") 
107 LOCATE (2,15):PRINT "TTHHEEMMEESS" 
108 LOCATE (3,15):PRINT "------------"  
109 CALL COLOR("0RW") 
110 LOCATE (6,2) 
111 PRINT TAB(2);"1.....................MOTS INVARIABLES" 
112 PRINT TAB(2);"2........................... ADJECTIFS" 
113 PRINT TAB(2);"3...............................VERBES" 
114 PRINT TAB(2);"4.................................NOMS" 
115 PRINT:PRINT " PRECISEZ :" 
116 LOCATE (12,11):PRINT "* VOTRE CHOIX ( DE 1 A 4 ) "; 
117 ACCEPT VALIDATE("1234")NULL(0)SIZE(1)BEEP;CHOIX:IF CHOIX=0 THEN 116 
118 LOCATE (12,38):PRINT USING"##";CHOIX 
119 LOCATE (13,11):PRINT "* DEBUT DES DATAS (->"; 
120 GOSUB 161 
121 I=0 
122 I=I+1:READ NO$,GE$,G$,MF$ 
123 IF NO$="FI"THEN I=I-1:GOTO 124 ELSE 122 
124 LOCATE (13,31):PRINT I;") "; 
125 ACCEPT VALIDATE(DIGIT)NULL(0)SIZE(3)BEEP;DEB:IF DEB>I THEN 124 
126 LOCATE (13,37):PRINT USING"### ";DEB 
127 LOCATE (14,11):PRINT "* CHOIX DU HASARD (1 A 10 )"; 
128 ACCEPT VALIDATE(DIGIT)NULL(0)SIZE(2)BEEP;H:IF H=0 THEN 116 
129 LOCATE (14,38):PRINT USING"##";H 
130 IF H>10 THEN 127 
131 IF H>I-DEB THEN H=I-DEB+1 
132 I=0 
133 GOSUB 161:FOR I=1 TO DEB-1:READ NO$,GE$,G$,M$:NEXT I 
134 RANDOMIZE:CALL COLOR("0RW"):HA=INTRND(H):FOR I=1 TO HA 
135 READ NO$,GE$,G$,MF$ 
136 IF NO$="FI"THEN 133 
137 NEXT I 
138 IF CH=4 THEN Q$=NO$:RJ$=MF$:GOTO 140 
139 Q$=MF$:RJ$=NO$ 
140 LOCATE (16,2):PRINT RPT$("         ",8) 
141 LOCATE (17,2):PRINT "TRADUIRE : ";:CALL COLOR("0bW"):PRINT Q$ 
142 CALL COLOR("0RW") 
143 LOCATE (22,2):PRINT "* MENU=M * TRADUCTION=T * SUITE=<--' *" 
144 CALL COLOR("0MW"):LOCATE (19,13):ACCEPT SIZE(27)BEEP;RE$ 
145 IF RE$="T"THEN GOSUB 149:GOTO 148 
146 IF RE$="M"THEN 57 
147 IF RE$<>RJ$THEN GOSUB 149:GOTO 140 ELSE 134 
148 LOCATE (19,13):PRINT RJ$:PAUSE 4:GOTO 134 
149 REM ******************************* 
150 REM * MEMORISATION DES ERREURS    * 
151 REM ******************************* 
152 IF Q$=MO$(FA)THEN 160 ELSE 153 
153 IF FA>39 THEN 154 ELSE FA=FA+1:MN$(FA)=RJ$:MO$(FA)=Q$:GOTO 160 
154 CLS:LOCATE (8,8):PRINT "J'AI 40 ERREURS EN MEMOIRES" 
155 LOCATE (11,5):PRINT "VOUS DEVEZ :" 
156 LOCATE (13,10):PRINT "- 1.REVISER " 
157 LOCATE (15,10):PRINT "- 2.OU VIDER LA MEMOIRE":LOCATE (18,5) 
158 PRINT "QUELLE EST VOTRE DECISION ? ";:ACCEPT VALIDATE("12")SIZE(1)NULL(1);D 
159 IF D=1 THEN 283 ELSE 319 
160 RETURN 
161 REM ******************************* 
162 REM *POSITIONNEMENT DANS LES DATAS* 
163 REM ******************************* 
164 ON CHOIX GOTO 167,166,168,165 
165 RESTORE 171:GOTO 169 
166 RESTORE 212:GOTO 169 
167 RESTORE 233:GOTO 169 
168 RESTORE 255 
169 RETURN 
170 GOTO 170 
171 REM ******************************* 
172 REM * DATA NOMS                   *
173 REM ******************************* 
174 DATA ANCILLA,AE,F,SERVANTE,DOMINA,AE,F,MAITRESSE,DER,AE,F,DEESSE 
175 DATA SICILIA,AE,F,SICILE,INSULA,AE,F,ILE,TERRA,RE,F,TERRE,VITA,AE,F,VIE 
176 DATA VILLA,AE,F,FERME,DILIGENTIA,AE,F,EMPRESSEMENT,ROSA,AE,F,ROSE 
177 DATA PUELLA,AE,F,JEUNE FILLE,CURA,AE,F,SOIN,PATRIA,AE,F,PATRIE 
178 DATA PUGNA,AE,BARTAILLE,PECUNIA,AE,FAIRGENT,COPIA,AE,F,ABONDANCE 
179 DATA FAMA,AE,F,RENOMMEE,SILVA,AE,F,FORET,FERA,AE,F,BETE SAUVAGE 
180 DATA SAGITTA,AE,F,FLECHE,SAPIENTIA,AE,F,SAGESSE,IRA,AE,F,COLERE 
181 DATA ARA,RE,F,AUTEL,CORONA,AE,F,COURONNE,GRAECIA,AE,F,GRECE 
182 DATA ITALIA,AE,F,ITALIE,STATUR,AE,F,STATUE,VIA,AE,F,ROUTE 
183 DATA MEDICUS,I,M,MEDECIN,FILIUS,I,M,PILS,CAMPUS,I,M,PLAINE,OCULUS,I,M,OEIL 
184 DATA SERVUS,I,M,ESCLAVE,EQUUS,I,M,CHEVAL,LUPUS,I,M,LOUP 
185 DATA AGNUS,I,M,AGNEAU,RIVUS,I,M,RUISSEAU,ROUR,RE,F,EAU,OPERA,RE,F,AIDE 
186 DATA SAEVITIA,RE,F,CRUAUTE,POPULUS,I,M,PEUPLE,ROMANI,ORUM,M,PL,ROMAINS 
187 DATA GRATIA,AE,F,RECONNAISSANCE,LRETITIR,RE,F,JOIE,MORBUS,I,M,MALADIE 
188 DATA RESCULAPIUS,I,M,ESCULAPE,ANNUS,I,M,ANNEE,PUER,I,M,ENFANT,LUDUS,I,M,JEU 
189 DATA SCHOLA,RE,F,ECOLE,MAGISTER,TRI,M,INSTITUTEUR,DISCIPULUS,I,M,ELEVE 
190 DATA LIBER,BRI,M,LIVRE,PIGRITIA,RE,F,PARESSE,RIPA,RE,F,RIVE 
191 DATA DIVITIRE,ARUM,F,PL,RICHESSES,HORTUS,I,M,JARDIN,MORA,RE,F,RETARD 
192 DATA CURA,RE,F,SOUCI,FABULA,RE,F,LEGENDE,BELLUM,I,N,GUERRE,DONUM,I,N,CADERU 
193 DATA FRUMENTUM,I,N,BLE,PERICULUM,I,N,PERIL,OPPIDUM,I,N,PLACE FORTE 
194 DATA PRAESIDIUM,I,N,GARNISON,RRMA,ARUM,N,ARMES,PRREMIUM,I,N,RECOMPENSE 
195 DATA IMPERIUM,I,N,EMPIRE,TEMPLUM,I,N,TEMPLE,RUXILIUM,I,N,SECOURS 
196 DATA OTIUM,I,N,LOISIR,FIGRICULTURA,RE,F,AGRICULTURE,AURUM,I,N,OR 
197 DATA MONUMENTUM,I,N,MONUMENT,SEPULCRUM,I,N,TOMBERU,INGENIUM,I,N,TALENT 
198 DATA TESTIMONIUM,I,N,PREUVE,ORACULUM,I,N,ORACLE,NEGOTIUM,II,N,AFFAIRE 
199 DATA VERBUM,I,N,PAROLE,LIBERI,ORUM,M,PL,LES ENFANTS,AUTUMNUS,I,M,AUTOMME 
200 DATA AEOLUS,I,M,EOLE,VEOTUS,I,M,VENT,ANTRUM,I,N,CAVERNE,CAELUM,I,N,CIEL 
201 DATA FOLIUM,II,N,FEUILLE,SCRIBA,AE,M,SCRIBE,AGRICOLA,AE,M,PAYSAN 
202 DATA NAUTA,AE,M,MARIN,ATHLETA,AE,M,ATHLETE,POETA,AE,M,POETE,HUMUS,I,F,SOL 
203 DATA COLLEGA,AE,M,COLLEGUE,FICUSA,I,F,FIGUIER,UMBRA,AE,F,OMBRE 
204 DATA AEGYPTUS,I,F,EGYPTE,PIRATA,AE,M,PIRATE,LAURUS,I,F,LAURIER 
205 DATA CONVIVA,AE,M,CONVIVE,INCOLA,AE,M,HABITANT,PERSA,AE,M,PERSE 
206 DATA SEQUANA,AE,M,LA SEINE,PLATANUS,I,F,PLATANE,VICTORIA,AE,F,VICTOIRE 
207 DATA FACTUM,I,N,ACTION,UNDA,AE,F,FLOT 
208 DATA FI,FI,FI,FI 
209 REM ******************************* 
210 REM DATA ADJECTIFS 
211 REM ******************************* 
212 DATA MAGNUS,FLUM,GRAND,FECUNDUS,A,UM,FERTILE,MULTI,AE,A,NOMBREUX 
213 DATA BONOS,R,UM,BON,ALBUSAR,UM,BLANC,JUCUNDUS,A,UM,AGREABLE 
214 DATA PULCHER,RA,RUM,BEAU,BERTUS,A,UM,HEUREUM,PROBUS,A,UM,HONNETE 
215 DATA EGREGIUS,A,UM,REMARQUABLE,GRAECUS,R,UM,GREC,ROMANUS,A,UM,ROMAIN 
216 DATA CLARUS,A,UM,CELESRE,IRATUS,A,UM,IRRITE,LAETUS,A,UM,JOYEUX 
217 DATA SALVUS,A,UM,SAUVE,PARVUS,A,UM,PETIT,PURUS,A,UM,PUR,SAEVUS,A,UM,CRUEL 
218 DATA MAXIMUS,A,UM,LE PLUS GRAND,BENIGNUS,R,UM,BIENVEILLANT,NATUS,A,UM,NE 
219 DATA MALUS,A,UM,MECHANT,NATUS+ACC.,A,UM,AGE DE,DOCTUS,A,UM,SAVANT 
220 DATA PIGER,GRA,GRUM,PARESSEUX,IMPIGER,GRA,GRUM,TRAVAILLEUR 
221 DATA MISER,A,UM,MISERABLE,LIBER,A,UM,LIBRE,ALTUS,A,UM.HAUT 
222 DATA TENER,A,UM,TENDRE,PROPINQUUS,A,UM,PROCHE,LONGUS,A,UM,LONG 
223 DATA INGRATUS,A,UM,INGRAT,REGER,RA,RUM,MALADE,PERITUS,A,UM,HABILE 
224 DATA VALIDUS,A,UM,ROBUSTE,OBSCURUS,A,UM,OBSCURE,ATER,RA,RUM,NOIR 
225 DATA NOVUS,A,UM,NOUVEAU,ANTIQUUS,A,UM,ANCIEN,NOSTER,RA,RUM,NOTRE 
226 DATA ARIDUS,A,UM,ARIDE,LONGINQUUS,A,UM,LOINTAIN,LATUS,R,UM,LARGE 
227 DATA MIRUS,A,UM,ADMIRABLE,INVITUS,A,UM,MALGRE(QUELQU'UN) 
228 DATA FRIGIDUS,A,UM,FROID 
229 DATA FI,FI,FI,FI 
230 REM ******************************* 
231 REM DATA MOTS INVARIABLES 
232 REM ******************************* 
233 DATA NON,-,-,NE PRS,ET,-,-,ET,SED,-,-,MAIS,ITAQUE,-,-,C'EST POURQUOI 
234 DATA NAM,-,-,CAR,IBI,-,-,LA 
235 DATA NON TANTUM SED ETIAM,-,-,NON SEULEMENT MAIS AUSSI 
236 DATA TAM,-,-,TELLEMENT,SAEPE,-,-,SOUVENT,IN(+ABLATIF),-,-.LIEU OU L'ON EST
237 DATA CUM(+ABLATIF),-,-,AVEC,IN(+ACC.),-,-.LIEU OU L'ON VA 
238 DATA AD(+ACC.Y,-,-,VERS,ANTE(+ACC.),-,-,AVANT.POST(+ACC.),-,-,APRES 
239 DATA INTER(+ACC.),-,-,ENTRE,PER(+ACC.),-,-,A TRAVERS 
240 DATA PROPTER(+ACC),-,-,A CAUSE DE,ADVERSUS(+ACC.),-,-,CONTRE
241 DATA APUD(+ACC.),-,-,CHEZ QUELQU-UN,PROPE(+ACC.),-,-,PRES DE
242 DATA NUMQUAM,-,-,NE...JAMAIS,JAM,-,-,DEJA,NUNC,-,-,MAINTENANT
243 DATA ETIAM,-,-,AUSSI,AUT,-,-,OU,SATIS,-,-,SUFFISAMENT,UBI,-,-,OU L'ON EST
244 DATA QUO ?,-,-,OU L'ON VA,QUARE ?,-,-,POURQUOI,CUR ?,-,-,POURQUOI
245 DATA QUOMODO ?,-,-,COMMENT,HODIE,-,-,AUJOURD'HUI,ALIQUANDO,-,-,QUELQUEFOIS
246 DATA ENIM,-,-,EN EFFET,SINE(+ABL.),-,-,SANS,PRO(+ABL.),-,-,DEVANT
247 DATA PRO(+ABL.),-,-,A LA PLACE DE,A(+ABL.),-,-,LOIN DE,AB(+ABL.),-,-,LOIN
248 DATA E(+ABL.),-,-,HORS DE,EX(+ABL.),-,-,HORS DE,DE(+ABL.),-,-,DU HAUT DE
249 DATA DE(+ABL.),-,-,AU SUJET DE,QUOTIDIE,-,-,CHAQUE JOUR,DEINDE,-,-,ENSUITE
250 DATA ITA,-,-,AINSI,QUOQUE,-,-,AUSSI,-NE,-,-,EST CE QUE,MOX,-,-,BIENTOT
251 DATA NONNE,-,-,EST CE QUE...NE...PAS,CUM,-,-,LORSQUE,NUM,-,-,EST CE QUE
252 DATA QUOD,-,-,PARCE QUE,NON JAM,-,-,NE PLUS,TUM,-,-,ALORS,TUNC,-,-,ALORS
253 DATA SEMPER,-,-,TOUJOURS
254 DATA FI,FI,FI,FI
255 REM ********************************
256 REM *        VERBES                *
257 REM ********************************
258 DATA SUM,ES,ESSE,ETRE,ADSUM,ES,ESSE,ETRE PRESENT,ABSUM,ES,ESSE,ETRE ABSENT
259 DATA DESUM,ES,ESSE,MANQUER A,PRAESUM,ES,ESSE,COMMANDER
260 DATA POSSUM,POTES,POSSE,POUVOIR,PROSUM,PRODES,PRODESSE,ETRE UTILE
261 DATA DELECTO,AS,ARE,CHARMER,TENEO,ES,ERRE,TENIR,AMO,AS,ARE,AMIER
262 DATA LAUDO,AS,ARE,LOUER,TIMEO,ES,ERE,CRAINDRE,ORO,AS,ARE,PRIER
263 DATA ORNO,AS,ARE,ORNER,VIDEO,ES,ERE,VOIR,AMBULO,AS,ARE,MARCHER
264 DATA NECO,AS,ARE,TUER,VULNERO,AS,ARE,BLESSER,PORTO,AS,ARE,PORTER
265 DATA INTRO,AS,ARE,ENTRER,CURO,AS,ARE,SOIGNER,DO,AS,ARE,DONNER
266 DATA HABITO,AS,ARE,HABITER,POTO,AS,ARE,BOIRE,IMMOLO,AS,ARE,IMMOLER
267 DATA VALEO,ES,ERE,ETRE,BIEN PORTANT,DEBEO,ES,ERE,DEVOIR
268 DATA STUDEO,ES,ERE,S'AAPLIQUER A,MANEO,ES,ERE,DEMEURER
269 DATA VITUPERO,AS,ARE,BLAMER,VERBERO,AS,ARE,FRAPPER,SEDEO,ES,ERE,ETRE ASSIS
270 DATA PUGNO,AS,ARE,COMBATTRE,ARO,AS,ARE,LABOURER,HABEO,ES,ERE,TENIR
271 DATA MONEO,ES,ERE,AVERTIR,LABORO,AS,ARE,TRAVAILLER,CELEBRO,AS,ARE,CELEBRER
272 DATA CANTO,AS,ARE,CHANTER,NARRO,AS,ARE,RACONTER,TERREO,ES,ERE,EFFRAYER
273 DATA AUGEO,ES,ERE,AUGMENTER,AEDIFICIO,AS,ARE,EDIFIER,COLLOCO,AS,ARE,PLACER
274 DATA ERRO,AS,ARE,SE TROMPER,ERRO,AS,ARE,ERRER,INTERROGO,AS,ARE,INTERROGER
275 DATA SUPERSUM,ES,ESSE,SUREVIVRE,INTERSUM,ES,ESSE,PARTICIPER
276 DATA NOCEO,ES,ERE,NUIRE A,EXCITO,AS,ARE,FAIRE SORTIR,SPECTO,AS,ARE,REGARDER
277 DATA NAVIGO,AS,ARE,NAVIGUER,PROPERO,AS,ARE,SE HATER
278 DATA FI,FI,FI,FI
279 DATA FIN,FIN,FIN,FIN
280 REM MOTS ENTRES JUSQU'A LA PAGE 51
281 REM *******************************
282 REM *   LISTE DES ERREURS         *
283 CLS:CALL COLOR("0RW")
284 LOCATE (2,10):PRINT "LISTE DES ERREURS :":PRINT
285 LOCATE (3,10):PRINT "-------------------":PRINT
286 IF FA<>0 THEN 289
287 LOCATE (12,7):PRINT "JE N'AI PAS ENCORE "
288 LOCATE (15,16):PRINT "ENREGISTRE D'ERREUR ":GOTO 296
289 LI=0
290 FOR I=1 TO FA
291 LI=LI+1
292 IF LI>15 THEN LI=0:PRINT TAB(35);"<--'":PAUSE ELSE 293
293 PRINT MO$(I);TAB(18);" --> "&MN$(I)
294 NEXT I
295 PRINT
296 LOCATE (22,35):PRINT "<--'":PAUSE
297 REM *******************************
298 REM * REVISION DES MOTS NON SUS   *
299 REM *******************************
300 CLS
301 CALL COLOR("0RWL"):LOCATE (2,13):PRINT "RREEVVIISSIIOONN"
302 LOCATE (3,13):PRINT "---------------- "
303 LOCATE (5,6):PRINT "DDEESS  MMOOTTSS  NNOONN  SSUUSS"
304 LOCATE (6,6):PRINT "--------------------------------"
305 CALL COLOR("0RW")
306 LOCATE (22,8):PRINT "* [T]=TRADUCTION * [M]=MENU *"
307 FOR I=1 TO FA
308 LOCATE (12,2):PRINT RPT$(" ",38)
309 LOCATE (12,2):PRINT "TRADUIRE : ";:CALL COLOR("0bW"):PRINT MO$(I)
310 LOCATE (15,13):PRINT RPT$(" ",27)
311 CALL COLOR("0RW"):LOCATE(15,13):ACCEPT BEEP;R$  
312 IF R$="M"THEN GOTO 57
313 IF R$=MN$(I)THEN 316
314 IF R$="T"THEN LOCATE (15,13):PRINT MN$(I):PAUSE 2:GOTO 316
315 GOTO 310
316 NEXT I
317 GOTO 307
318 REM *******************************
319 REM * REMISE A ZERO MEMO ERREURS  *
320 REM *******************************
321 CLS "bBB":CALL COLOR("0RWHLF")
322 LOCATE (13,10):PRINT "RR..AA..ZZ  FFAAIITTEE "
323 LOCATE (14,10):PRINT "RR..AA..ZZ  FFAAIITTEE ":PAUSE 3
324 RUN 57