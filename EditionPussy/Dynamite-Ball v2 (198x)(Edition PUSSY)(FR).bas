1 CLS "BCG":CALL POKE(50500,162,5,45,162,136,45,10)
2 CALL POKE(50688,165,8,6,10):DIM B$(22,41)
3 CALL POKE(49156,198,0)
4 TIC1$="L,0D7C74EFBAB602FC"
5 TIC2$="L,0D7C35EDBAB602FC"
6 TIC3$="L,0D7C4EFABAB602FC"
7 CALL CHAR(111,"0204187E7EFFFF7E7E18")
8 CALL CHAR(60,"3F40403F000000000000")
9 CALL CHAR(61,"FF0000FF000000000000")
10 CALL CHAR(62,"FC0202FC000000000000")
11 CALL CHAR(97,"007C7C5C5C5C5C5C5C5C")
12 CALL CHAR(98,"5C5C5C7C101008080402")
13 CALL CHAR(35,"7E8BA1D593A9CB91CB7E")
14 CALL CHAR(36,"7E8181817EE7181818E7")
15 ZIP$="0DFD3FDCCD6C6FB4A2FF":VIE=2:GOSUB 70
16 CALL COLOR("0WBHL"):LOCATE (10,15):PRINT "GGOO  !!!!!!"
17 CALL COLOR("0RBHL"):PRINT:LOCATE (11,15):PRINT "GGOO  !!!!!!"
18 EX1$="0BA02163031B18008D1E139840073A108100F0901E81080480478F084420020570"
19 EX2$="CEBD02110840704E118840043CE0010F78C00316B080052C20010948800214A000"
20 FOR I=255 TO 9 STEP -.5:CALL POKE(258,I,132):NEXT I:VI=1:PBO=4:Q=1:BO=0:BN=0
21 EX$=EX1$&EX2$&"05284081FF":NIV=0:RANDOMIZE:CALL POKE(259,45)
22 CLS:NIV=NIV+1:BR=INTRND(2):IF BR=1 THEN BR$=CHR$(35) ELSE BR$=CHR$(36)
23 IF NIV=17 THEN NIV=16
24 CALL COLOR("1MC"):IF NIV<5 OR NIV>9 THEN GOTO 27
25 FOR H=4 TO 3+NIV:FOR L=1 TO 40:B$(H,L)="NO":NEXT L:NEXT H
26 ON NIV-4 GOSUB 76,92,89,79,83:GOTO 29
27 FOR H=4 TO 3+NIV:FOR L=1 TO 40:B$(H,L)="OK"
28 LOCATE (H,L):PRINT BR$:NEXT L:NEXT H:H=4+NIV
29 DIREC=4:L=INTRND(7)+NIV:CALL COLOR("1MC")
30 RAQ=19:CALL COLOR("1WC"):LOCATE (22,RAQ):PRINT "<"&RPT$("=",Q)&">"
31 CALL COLOR("1BC"):LOCATE (H,L):PRINT "o"
32 CALL COLOR("1WC"):FOR F=1 TO VI:CALL KEY1(A,B)
33 IF A=131 THEN GOSUB 56 ELSE IF A=129 THEN GOSUB 58 ELSE IF A=80 THEN V$=KEY$
34 NEXT F:IF H=4 AND B$(H,L)="NO"THEN RESTORE:GOTO 44
35 READ MUS:IF MUS=3 THEN RESTORE:GOTO 35
36 CALL POKE(258,MUS,132):IF H>21 THEN CALL SPEECH(TIC1$):GOTO 95
37 IF B$(H,L)="OK"THEN CALL SPEECH(TIC1$):B$(H,L)="NO":GOSUB 60
38 IF H=21 AND (L>=RAQ AND L<=RAQ+Q+1)THEN CALL SPEECH(TIC3$):GOSUB 82
39 IF H=21 AND (L=RAQ-1 OR L=RAQ+Q+2)THEN CALL SPEECH(TIC3$):GOSUB 67
40 IF L<2 THEN CALL SPEECH(TIC2$):DIREC=DIREC+1
41 IF L>39 THEN CALL SPEECH(TIC2$):DIREC=DIREC-1
42 LOCATE (H,L):PRINT " ":ON DIREC GOSUB 63,64,65,66
43 GOTO 31
44 CALL POKE(259,45):LOCATE (H,L):PRINT " ":BO=BO+50:CALL SPEECH("L,"&ZIP$)
45 FOR I=L*8 TO 30 STEP -1:CALL PLOT("W",I,0):FOR J=1 TO 15:NEXT J
46 CALL PLOT("C",I,0):NEXT I:CALL SPEECH("L,"&EX$):CALL HROFF:CALL COLOR("0BC")
47 IF BN<20*NIV THEN BN=0:GOSUB 70:GOTO 22
48 LOCATE (6,9):PRINT "VOTRE CHOIX BONUS ?":LOCATE (10,3):PRINT "_1] VITESSE"
49 PRINT:PRINT "  _2] LARGEUR":PRINT:PRINT "  _3] BONUS"
50 BR$=KEY$:IF NOT NUMERIC(BR$)THEN 50 ELSE BR=VAL(BR$):IF BR=0 OR BR>3 THEN 50
51 IF BR=1 THEN VI=VI+1 ELSE IF BR=2 THEN Q=Q+1: ELSE PBO=PBO+1
52 BN=0:GOSUB 70:GOTO 22
53 CALL COLOR("0BC"):LOCATE (8,10):PRINT "VOTRE SCORE:";BO
54 LOCATE (12,6):PRINT "UNE AUTRE PARTY ?":TIT$=KEY$:IF TIT$="O"THEN RUN
55 IF TIT$="N"THEN CALL HROFF:CLS "BCC":PAUSE 0:END ELSE 54
56 IF RAQ<2 THEN RETURN
57 RAQ=RAQ-1:LOCATE (22,RAQ):PRINT "<"&RPT$("=",Q)&"> ":RETURN
58 RAQ=RAQ+1:IF RAQ+Q+1>39 THEN RAQ=RAQ-1:RETURN
59 LOCATE (22,RAQ-1):PRINT " <"&RPT$("=",Q)&">":RETURN
60 BO=BO+PBO:BN=BN+1:DIREC=DIREC+2:CALL COLOR("0RC"):LOCATE (1,31):PRINT BO
61 IF DIREC>4 THEN DIREC=DIREC-4
62 RETURN
63 H=H-1:L=L-1:RETURN
64 H=H-1:L=L+1:RETURN
65 H=H+1:L=L-1:RETURN
66 H=H+1:L=L+1:RETURN
67 IF L=40 THEN LOCATE (H,L):PRINT " ":DIREC=1:H=20:L=39:RETURN
68 IF DIREC=4 THEN DIREC=1 ELSE DIREC=2
69 RETURN
70 CALL HRON("C",3,1):CALL EXEC(50500)
71 CALL LINE("R",7,0,319,0)
72 CALL COLOR("0RCH"):TIT$="DYNAMITE-BALL":LOCATE (1,6):PRINT TIT$
73 LOCATE (2,6):PRINT TIT$:CALL COLOR("1RC"):LOCATE (1,25):PRINT "BONUS:"
74 LOCATE (1,1):PRINT "aaaa":LOCATE (2,1):PRINT "bbbb":CALL COLOR("1WC")
75 LOCATE (2,25):PRINT RPT$("<=>  ",VIE):RETURN
76 FOR I=1 TO 36 STEP 6:FOR J=1 TO 4:B$(6,I+J)="OK"
77 LOCATE (6,I+J):PRINT BR$:NEXT J:NEXT I:H=9:FOR I=1 TO 40:B$(4,I)="OK"
78 LOCATE (4,I):PRINT BR$:B$(8,I)="OK":LOCATE (8,I):PRINT BR$:NEXT I:RETURN
79 FOR H=4 TO 12:FOR I=1 TO 40:B$(H,I)="OK":LOCATE (H,I):PRINT BR$:NEXT I
80 NEXT H:H=13
81 FOR I=1 TO 40:B$(8,I)="NO":LOCATE (8,I):PRINT " ":NEXT I:RETURN
82 DIREC=DIREC-2:LOCATE (H,L):PRINT " ":H=H-1:RETURN
83 L=0:H=5
84 FOR J=1 TO 4
85 FOR I=1 TO 5:GOSUB 66:LOCATE (H,L):PRINT BR$:B$(H,L)="OK":NEXT I
86 FOR I=1 TO 5:GOSUB 64:LOCATE (H,L):PRINT BR$:B$(H,L)="OK":NEXT I
87 NEXT J:H=14:FOR I=1 TO 40:LOCATE (4,I):PRINT BR$:B$(4,I)="OK"
88 FOR J=11 TO 13:LOCATE (J,I):PRINT BR$:B$(J,I)="OK":NEXT J:NEXT I:RETURN
89 FOR I=1 TO 170:H=INTRND(6)+4:L=INTRND(40):LOCATE (H,L):PRINT BR$
90 B$(H,L)="OK":NEXT I:FOR I=1 TO 40:LOCATE (11,I):PRINT BR$:B$(11,I)="OK"
91 LOCATE (4,I):PRINT BR$:B$(4,I)="OK":NEXT I:H=12:RETURN
92 FOR H=4 TO 10 STEP 2:FOR L=H-3 TO 40-H+4
93 LOCATE (H,L):PRINT BR$:B$(H,L)="OK"
94 NEXT L:NEXT H:H=11:RETURN
95 CALL POKE(259,45):RESTORE:VIE=VIE-1:IF VIE=-1 THEN GOTO 53
96 NIV=NIV-1:GOSUB 70:GOTO 22
97 DATA 199,176,158,133,133,118,133,158,199,176,158,158,176,199,176,199,176
98 DATA 158,133,133,118,133,158,199,176,158,158,176,176,199,149,149,149,118
99 DATA 118,118,133,133,158,199,176,199,176,158,133,133,118,133,158,199,176
100 DATA 158,158,176,176,199
101 DATA 133,118,133,149,158,149,133,176,158,149,158,149,133,133,118,133
102 DATA 149,158,149,133,176,133,158,176,199
103 DATA 199,199,176,199,149,158,199,199,176,199,133,149,199,199,118,149
104 DATA 158,176,199,199,118,149,133,149
105 DATA 199,199,133,133,118,118,133,149,149,158,158,176,176,199,133,133
106 DATA 149,149,158,158,176,133,133,149,149,158,158,176,199,199,133
107 DATA 133,118,118,133,149,149,158,158,176,176,199,3
