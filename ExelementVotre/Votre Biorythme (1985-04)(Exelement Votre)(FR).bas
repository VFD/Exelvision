60 DIM J(12),MO$(12)
100 FOR M=1 TO 12
110 READ J(M),MO$(M)
120 NEXT
130 DATA 0,JANVIER,31,FEVRIER,59,MARS,90,AVRIL,120,MAI,151,JUIN,181,JUILLET
140 DATA 212,AOUT,243,SEPTEMBRE,273,OCTOBRE,304,NOVEMBRE,334,DECEMBRE
150 CLS "CBB"
170 CALL HRON("B",1,16)
180 LOCATE (1,17):CALL COLOR("0CB")
190 PRINT "Donnez votre date de naissance : "
200 INPUT "Jour : ";JN,"Mois : ";MN,"Annee : ";AN
210 INPUT "Biorythme sur combien de mois: ";NBM,"a partir de quel mois : ";MDP
220 INPUT "pour quelle annee : ";AA
230 JN=JN-1:JT=(INT((AA-AN)*365.25))+J(MDP)-J(MN)-JN
240 JF=JT+NBM*30.4
250 CALL LINE("R",0,80,319,80)
260 FOR I=0 TO NBM
270 AX=I*319/NBM:BX=AX+319/3/NBM:CX=AX+319*2/3/NBM
280 CALL LINE("R",AX,0,AX,159)
290 IF I<>NBM THEN CALL LINE("b",BX,0,BX,159):CALL LINE("b",CX,0,CX,159)
300 NEXT
310 CLS
320 PRINT "BIORYHTME DE ";MO$(MDP);" A ";MO$(MDP+NBM-1);" ";AA
330 CALL COLOR("0YB"):PRINT "Courbes physique                       "
340 CALL COLOR("0MB"):PRINT "Courbe sentimentale                    "
350 CALL COLOR("0WB"):PRINT "Courbe intellectuelle                  "
360 CALL TRA("Y",23,JT,JF,NBM)
370 CALL TRA("M",28,JT,JF,NBM)
380 CALL TRA("W",33,JT,JF,NBM)
390 CALL COLOR("0CB")
400 PRINT "Pour une autre periode, presser la barre d'espacement "
410 CALL KEY1(K,L)
420 IF L<>1 THEN 410
430 IF K=32 THEN CALL HRON("B",1,16):GOTO 210 ELSE 150
440 SUB TRA(C$,T,JT,JF,NBM)
450 X1=0:Y1=159*(1-SIN(JT*2*PI/T))/2
460 FOR XB=JT TO JF STEP 2/NBM
470 X=(XB-JT)*319/(JF-JT)
480 YB=SIN(XB*2*PI/T)
490 Y=159*(1-YB)/2
500 CALL LINE(C$,X1,Y1,X,Y)
510 X1=X:Y1=Y
520 NEXT
530 SUBEND
